//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_DATE($1)//Date of journal to find
C_TEXT($2)//Journal Type, INC,EXP,REV,PMT,PRE,RPR
C_LONGINT($Period)//Period number of date
$Period:=GetPeriod ($1)
If ($Period>0)
QUERY([Journal];[Journal]PeriodNo=$Period;*)
QUERY([Journal]; & ;[Journal]JrnlType=$2)
If (Records in selection([Journal])=0)
READ WRITE([Journal])
CREATE RECORD([Journal])
[Journal]JournalNo:=GetNextJrnlNo 
[Journal]PeriodNo:=$Period
[Journal]JrnlType:=$2
Case of 
: ([Journal]JrnlType="INC")
[Journal]Narration:=Get localized string("Income Journal")
: ([Journal]JrnlType="EXP")
[Journal]Narration:=Get localized string("Expense Journal")
: ([Journal]JrnlType="REC")
[Journal]Narration:=Get localized string("Receipts Journal")
: ([Journal]JrnlType="PMT")
[Journal]Narration:=Get localized string("Payments Journal")
: ([Journal]JrnlType="PRE")
[Journal]Narration:=Get localized string("Prepaid Rent Journal")
: ([Journal]JrnlType="RPR")
[Journal]Narration:=Get localized string("Reverse Prepaid Rent Journal")
Else 
[Journal]Narration:=""
End case 
SAVE RECORD([Journal])
READ ONLY([Journal])
LOAD RECORD([Journal])
End if 
Else 
//Journal is not inside any period!!!!  
AlertMessage (Get localized string("A serious problem has occurred as the date is not inside any journal - please contact Technical Support for assistance."))
End if 