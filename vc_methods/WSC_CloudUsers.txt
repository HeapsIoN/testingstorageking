//%attributes = {"lang":"en"} comment added and reserved by 4D.

// ----------------------------------------------------
// User name (OS): jonathon.wildon
// Date and time: 20/02/16, 17:31:12
// ----------------------------------------------------
// Method: WSC_CloudUsers
// Description
//
// This method is used to send users and permissions to 
// Storman Hub by calling WS_CloudUser.
//
// Parameters
// ----------------------------------------------------


If (Records in selection([Facility]))=0
SetFacility 
End if 

C_TEXT($1;$2;$3;$4;$5;$6;$7;$9;$10;$11)
C_BOOLEAN($0;WS_ORSuccess)
C_LONGINT(WS_ORErrorCode)
C_TEXT(WS_ORErrorDescription)
C_POINTER($8)

$0:=False

WEB SERVICE SET PARAMETER("WS_Username";$1)
WEB SERVICE SET PARAMETER("WS_UserID";$2)
WEB SERVICE SET PARAMETER("WS_Password";$3)
WEB SERVICE SET PARAMETER("WS_FullName";$4)
WEB SERVICE SET PARAMETER("WS_Language";$5)
WEB SERVICE SET PARAMETER("WS_Actiontext";$6)
WEB SERVICE SET PARAMETER("WS_Permissions";$7)
WEB SERVICE SET PARAMETER("WS_asUserGroups";$8->)
WEB SERVICE SET PARAMETER("WS_AccessFacility";$9)
WEB SERVICE SET PARAMETER("WS_ORFacilityCode";$10)
WEB SERVICE SET PARAMETER("WS_WebServicePassword";$11)

$PortPos:=Position(":";[Facility]CentralAddress)
If ($PortPos>0)
$PortString:=Substring([Facility]CentralAddress;$PortPos+1)
Else 
$PortString:="19812"
End if 
$Port:=Num($PortString)

WEB SERVICE CALL("https://"+GetDNS ("MV")+":"+$PortString+"/4DSOAP/";"Storman#WS_CloudUsers";"WS_CloudUsers";"http://www.storpay.com/webservices";Web Service dynamic)

If (OK=1)
WEB SERVICE GET RESULT(WS_ORSuccess;"WS_ORSuccess")
WEB SERVICE GET RESULT(WS_ORErrorCode;"WS_ORErrorCode")
WEB SERVICE GET RESULT(WS_ORErrorDescription;"WS_ORErrorDescription";*)// Memory clean-up on the final return value.
End if 

$0:=WS_ORSuccess
