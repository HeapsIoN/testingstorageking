//%attributes = {"lang":"en"} comment added and reserved by 4D.

// ----------------------------------------------------
// User name (OS): nicholas.wykes
// Date and time: 09/12/09, 16:28:50
// ----------------------------------------------------
// Method: HTTP_GoogleGeocode
// Description
// This method is used to parse an entered human readable address and return a geocoded (coordinate) response
//
// Parameters
//$1 - String - Address to parse
//$0 - String - Returned Coordinates in xx.xxxx,-xx.xxxx format or an error
//$2 - String - Google Maps API Key
// ----------------------------------------------------


C_TEXT($0;$1)
C_LONGINT($TCPID;$Error;$Pos;$TCP_ReceiveError)
C_TEXT($XMLResult)

$TCPID:=0
$Buffer:=""
$Status:=0
$TCP_ReceiveError:=0
$Response:=""
$Header:=""
$Message:=""
$cr:=Char(13)+Char(10)
$TransactionResponse:=""

$Server:="maps.google.com"

$Address:=Replace string($1;" ";"+")


$Header:=$Header+"GET /maps/geo?q="+$Address+"&output=xml&sensor=false&key="+<>sHOGoogleAPI+" HTTP/1.1 "+$cr
$Header:=$Header+"Host: "+$Server+$cr+$cr

$Error:=TCP_Open ($Server;80;$TCPID;0)
If ($Error=0)
$Error:=TCP_Send ($TCPID;$Header)
$Reply:=""
$WaitTime:=Current time+?00:00:15?//Timeout after 15 seconds

Repeat //While a HTTP response is being received
$Error:=TCP_Receive ($TCPID;$Buffer)
$resp:=OK
$Error:=TCP_State ($TCPID;$Status)
$Reply:=$Reply+$Buffer
If (Current time>$WaitTime)
$Error:=1//Timeout
End if 
Until ($Status=0) | ($Error>0) | (($Buffer="") & (Length($Reply)>10))
$Message:=$Reply


$Error:=TCP_Close ($TCPID)
If ($Message="")
$0:="ERRTCP"
Else 
//Strip headers somewhere in here
$HTTPResponseCode:=""
Repeat //While a complete HTTP request and response is being received
$HTTPResponse:=Substring($Message;1;Position($cr+$cr;$Message)-1)
If (Position("HTTP/";$HTTPResponse)>0)
$HTTPResponseCode:=Substring($HTTPResponse;Position(" ";$HTTPResponse)+1;3)//This will be e.g. 100 (Continue), 200 (OK) etc
$Message:=Substring($Message;Position($cr+$cr;$Message)+Length($cr+$cr))
Else 

$NewPos:=1
Repeat 
$NewPos:=Position("\n";$Message;$NewPos;*)
$NextBracket:=Position("<";$Message;$NewPos)
If (Substring($Message;$NextBracket;1)="<")
$EndPos:=Position("\n";$Message;$NewPos+1;*)
$XMLResult:=$XMLResult+Substring($Message;$NewPos+1;$EndPos-$NewPos)
$NewPos:=$EndPos
End if 

Until (Position("<";$Message;$NewPos)=0)



Case of 
: (Num($HTTPResponseCode)>=200) & (Num($HTTPResponseCode)<300)
$TransactionResponse:=$Message
Else 
$TransactionResponse:=" "//To Break out of the Until loop
$Error:=Num($HTTPResponseCode)
End case 
End if 
Until ($TransactionResponse#"")

$XMLRef:=DOM Parse XML variable($XMLResult)

//Parse the result from Google to recover reponse status code
$vElement:=DOM Find XML element($XMLRef;"/kml/Response/Status/code")
DOM GET XML ELEMENT VALUE($vElement;$GError)

If ($GError="200")
$vElement:=DOM Find XML element($XMLRef;"/kml/Response/Placemark/Point/coordinates")
DOM GET XML ELEMENT VALUE($vElement;$Message)
$Pos:=Position(",";$Message)
$Long:=GetTextNo ($Message;1;",")
$Lat:=GetTextNo ($Message;2;",")
$0:=$Lat+","+$Long
Else 
Case of 
: ($GError="500")
$0:=Get localized string("Error - Geocoding Server Error - Request could not be processed")
: ($GError="601")
$0:=Get localized string("Error - The Address field has not been completed")
: ($GError="602")
$0:=Get localized string("Error - Unknown Address - the address could not be located - check spelling or enter a less specific address")
: ($GError="603")
$0:=Get localized string("Error - The Geocode could not be returned for the entered address due to legal or contractual reasons. Please contact Google Support if you believe this to be in error.")
: ($GError="610")
$0:=Get localized string("Error - Bad Key - The key entered in Storman Setup is either invalid or does not match the issued domain.")
: ($GError="620")
$0:=Get localized string("Error - Too many requests - The key entered has gone over the request limit in the 24hr period or has submitted too many requests too soon. Please try again later.")
Else 
$0:=Get localized string("Error - Unknown issue.")
End case 
End if 

DOM CLOSE XML($XMLRef)
End if 
End if 
$Error:=TCP_Close ($TCPID)
