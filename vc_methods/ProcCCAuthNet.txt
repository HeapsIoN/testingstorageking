//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_TEXT($0)//Authorization Code
C_BOOLEAN($1)//$1 = Display Decline / Error Alerts
C_TEXT($2)//$2 P=Process, C=Credit, V=Void
C_REAL($3)//$1 = Transaction Amount
C_TEXT($4;$5)//$2 = Card Number & Expiry Date
C_TEXT($6)//TransID
C_TEXT($7;$8;$9;$10;$11;$12;$13)//Invoice,Description,CustomerCode,CustName,Address,City,ZIP
C_TEXT($14;$15;$16)//Blank,Login,Password
//C_LONGINT($17;$18)  `Logging,Timeout
C_TEXT($Message;$Header;$Response)
C_LONGINT($TCPID;$Error)

$Status:=0
$Buffer:=""
$TCPID:=0
$cr:=Char(13)+Char(10)

If ([Facility]TestAccount)// - 10221
$ServerAddress:="test.authorize.net"
Else 
$ServerAddress:="secure.authorize.net"
End if 


$Folder:="/gateway/transact.dll"
$ServerPort:="443"
//$Error:=IT_MyTCPAddr ($ClientAddress;$ClientSubnet)

//Build the transaction message
$Message:="SubmitPayment="//The Name of the HTML form
$Message:=$Message+"&x_Login="+$15
$Message:=$Message+"&x_Password="+$16
$Message:=$Message+"&x_Version="+"3.0"
$Message:=$Message+"&x_ADC_Delimited_Data=TRUE"
$Message:=$Message+"&x_ADC_URL=FALSE"
If ($2="P")
$Message:=$Message+"&x_Type=AUTH_CAPTURE"
Else 
If ($2="C")
$Message:=$Message+"&x_Type=CREDIT"
Else 
$Message:=$Message+"&x_Type=VOID"//If same day and amount matches
End if 
$Message:=$Message+"&x_Trans_ID="+$6
End if 
$Message:=$Message+"&x_Amount="+String(Round($3;2))
$Message:=$Message+"&x_Card_Num="+$4
$Message:=$Message+"&x_Exp_Date="+Substring($5;1;2)+"/"+Substring($5;3;2)
$Message:=$Message+"&x_Invoice_Num="+$7
$Message:=$Message+"&x_PO_Num="+$7
$Message:=$Message+"&x_Description="+$8
$Message:=$Message+"&x_Cust_ID="+$9
$Message:=$Message+"&x_Company="+$10
$Message:=$Message+"&x_Address="+$11
$Message:=$Message+"&x_Zip="+$13
$Message:=$Message+"&x_Echo_Data=TRUE"
$Message:=$Message+"&x_Method=CC"

$Header:=""
$Header:=$Header+"POST "+$Folder+" HTTP/1.1"+$cr
$Header:=$Header+"Host: "+$ServerAddress+":"+$ServerPort+$cr
$Header:=$Header+"Accept: "+"*/*"+$cr
$Header:=$Header+"Pragma: no-cache"+$cr
$Header:=$Header+"Connection: Keep-Alive"+$cr
$Header:=$Header+"Extension: Security/Remote-Passphrase"+$cr
$Header:=$Header+"Content-length: "+String(Length($Message))+$cr

$Error:=TCP_Open ($ServerAddress;Num($ServerPort);$TCPID;3)

If ($Error=0)
MaskCCLog ($Message;"AuthorizeNet")
$Error:=TCP_Send ($TCPID;$Header+$cr+$Message+$cr+$cr)
If (False)
$Doc:=Create document("AuthNet.txt")
SEND PACKET($Doc;$Header+$cr+$Message+$cr+$cr)
CLOSE DOCUMENT($Doc)
End if 
$Message:=""
$WaitTime:=Current time+?00:00:10?//Timeout after 10 seconds

Repeat //While a single HTTP response is being received
$Error:=TCP_Receive ($TCPID;$Buffer)
$Error:=TCP_State ($TCPID;$Status)
$Message:=$Message+$Buffer
Until ($Status=0) | ($Error>0) | (Current time>$WaitTime)
$Error:=TCP_Close ($TCPID)

$Response:=""
Case of 
: ($Error#0)
$Message:="4DIC TCP Error # "+String($Error)
: (Current time>$WaitTime)
$Message:=Get localized string("Timeout error")
Else 
Repeat //While a complete transaction request and response is being received
$HTTPResponse:=Substring($Message;1;Position($cr+$cr;$Message)-1)
If (Position("HTTP";$HTTPResponse)>0)
$HTTPResponseCode:=Substring($HTTPResponse;Position(" ";$HTTPResponse)+1;3)//This will be e.g. 100 (Continue), 200 (OK) etc
$Message:=Substring($Message;Position($cr+$cr;$Message)+Length($cr+$cr))
Else 
//If not an HTTPResponse Header then must be the Authorize Net 
//Payment Response message content
If (Num($HTTPResponseCode)>=200) & (Num($HTTPResponseCode)<300)
$Response:=$Message
End if 
End if 
Until ($Response#"")
End case 
//Assign Authorize Net response string values into an array
ARRAY TEXT($atResponse;0)
Repeat 
$Pos:=Position(",";$Response)
If ($Pos>0)
INSERT IN ARRAY($atResponse;Size of array($atResponse)+1)
$atResponse{Size of array($atResponse)}:=Replace string(Substring($Response;1;$Pos-1);Char(Double quote);"";*)
End if 
$Response:=Substring($Response;$Pos+1)
Until ($Pos=0)
INSERT IN ARRAY($atResponse;Size of array($atResponse)+1)
$atResponse{Size of array($atResponse)}:=Replace string($Response;Char(Double quote);"";*)
$Response:=""

//$0:=Num($atResponse{3})
//Responses:
//1,1,1,This transaction has been approved.,000000,P,0,,,1.00,CC,auth_capture,,,
//,,,,,,,,,,,,,,,,,,,,,,,225154C399B055DAE6B2EE7870110DA8,TRUE
//{1}Response Code=1Accepted,2Declined,3Error
//{2}Internal Code=Not used
//{3}ReasonCode=1Accepted,etc
//{4}ReasonText
//{5}AuthorisationCode
//{6}AddressVerification
//{7}TransID
//{8}EchoInvoiceNum etc

$0:=""
Case of 
: ($atResponse{1}="1")
$0:=$atResponse{5}//Return the authorization number
[Trxs]CCTransID:=$atResponse{7}//Return the TransID
: ($atResponse{1}="2")
If ($1)
AlertMessage (Get localized string("Card Declined")+" - "+$atResponse{3}+":"+$atResponse{4})
End if 
$0:="DEC"+$atResponse{3}//Declined return reason
vCCText:=$atResponse{4}
: ($atResponse{1}="3")
If ($1)
AlertMessage (Get localized string("Card Processing Error")+" - "+$atResponse{3}+":"+$atResponse{4})
End if 
$0:="ERR"+$atResponse{3}//Error return reason
vCCText:=$atResponse{4}
Else 
If ($1)
AlertMessage ($Message)
End if 
$0:="ERROR"
End case 
If (Size of array($atResponse)>=4)
vCCText:=$atResponse{4}
End if 
Else 
AlertMessage (Get localized string("The TCP connection could not be opened. Error")+" #"+String($Error))
$0:="ERR4IC"
End if 