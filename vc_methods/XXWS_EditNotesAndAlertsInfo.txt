//%attributes = {"lang":"en"} comment added and reserved by 4D.
SOAP DECLARATION(WS_vtAgreementNumber;Is text;SOAP input;"WS_vtAgreementNumber")
SOAP DECLARATION(WS_vtPassword;Is text;SOAP input;"WS_vtPassword")
SOAP DECLARATION(WS_IsWebID;Is Boolean;SOAP input;"WS_IsWebID")
SOAP DECLARATION(WS_IsAgreement;Is Boolean;SOAP input;"WS_IsAgreement")

SOAP DECLARATION(WS_ORSuccess;Is Boolean;SOAP output;"WS_ORSuccess")
SOAP DECLARATION(WS_ORErrorCode;Is longint;SOAP output;"WS_ORErrorCode")
SOAP DECLARATION(WS_ORErrorDescription;Is text;SOAP output;"WS_ORErrorDescription")
SOAP DECLARATION(WS_CustomerNotes;Is text;SOAP input;"WS_CustomerNotes")
SOAP DECLARATION(WS_AlertNotesMsg;Is Boolean;SOAP input;"WS_AlertNotesMsg")


$String:="Start WS_GetNotesAndAlertsInfo"+Char(13)
$String:=$String+"WS_vtAgreementNumber: "+WS_vtAgreementNumber+Char(13)
$String:=$String+"WS_vtPassword: "+WS_vtPassword+Char(13)
$String:=$String+"WS_IsWebID: "+String(WS_IsWebID)+Char(13)

WSRECORDLOG ($String)

READ WRITE([Customer])

WS_ORSuccess:=True
WS_ORErrorDescription:=""
WS_IsWebID:=False

If (WS_IsWebID=False)
If (WS_IsAgreement=True)
QUERY([Agreement];[Agreement]RAgreeNo=WS_vtAgreementNumber)
Else 
QUERY([Reservation];[Reservation]RReserveNo=WS_vtAgreementNumber)
End if 
Case of 
: (Records in selection([Agreement])=0) & (Records in selection([Reservation])=0)
WS_ORSuccess:=False
WS_ORErrorCode:=1//Agreement doesn't exist.
WS_ORErrorDescription:="No agreement or reservation found"
: (Records in selection([Agreement])=1) | (Records in selection([Reservation])=1)
If (WS_IsAgreement=True)
RELATE ONE([Agreement]CustomerCode)
Else 
RELATE ONE([Reservation]CustomerCode)
End if 

If ([Customer]Password=WS_vtPassword)
If (Records in selection([Customer])=1) & (Locked([Customer])=False)
If ((Records in selection([Agreement])=1) & (Locked([Agreement])=False)) | ((Records in selection([Reservation])=1) & (Locked([Reservation])=False))
[Customer]Notes:=WS_CustomerNotes
SAVE RECORD([Customer])
If (WS_IsAgreement=True)
[Agreement]AlertNotesMsg:=WS_AlertNotesMsg
SAVE RECORD([Agreement])
READ ONLY([Agreement])
UNLOAD RECORD([Agreement])
Else 
[Reservation]AlertNote:=WS_AlertNotesMsg
SAVE RECORD([Reservation])
READ ONLY([Reservation])
UNLOAD RECORD([Reservation])
End if 
Else 
WS_ORSuccess:=False
WS_ORErrorCode:=7//Record locked or not found
WS_ORErrorDescription:="Agreement or reservation locked or not found"
End if 

Else 
If (Locked([Customer]))
LOCKED BY([Customer];$Process;$User;$Machine;$ProcName)
WS_ORSuccess:=False
WS_ORErrorCode:=5//Record locked
WS_ORErrorDescription:="Records in Selection= "+String(Records in selection([Customer]))+" Read/Write= "+String(Read only state([Customer]))+" Locked= "+String(Locked([Customer]))+"Details: "+String($Process)+" "+String($User)+" "+String($Machine)+" "+String($ProcName)
Else 
WS_ORSuccess:=False
WS_ORErrorCode:=6// No records in selection
WS_ORErrorDescription:="Records in Selection= "+String(Records in selection([Customer]))+" Read/Write= "+String(Read only state([Customer]))+" Locked= "+String(Locked([Customer]))
End if 
End if 
Else 
WS_ORSuccess:=False
WS_ORErrorCode:=2//Customer password doesn't match one given. 
WS_ORErrorDescription:="Invalid customer password."
End if 
Else 
WS_ORSuccess:=False
WS_ORErrorCode:=4//There is a duplicate of agreement. 
WS_ORErrorDescription:="There are duplicate agreements."
End case 
End if 

$String:="End WS_GetNotesAndAlertsInfo"+Char(13)
$String:=$String+"WS_vtAgreementNumber: "+String(WS_vtAgreementNumber)+Char(13)
$String:=$String+"WS_ORSuccess: "+String(WS_ORSuccess)+Char(13)
$String:=$String+"WS_ORErrorCode: "+String(WS_ORErrorCode)+Char(13)
$String:=$String+"WS_ORErrorDescription: "+WS_ORErrorDescription+Char(13)
$String:=$String+"WS_CustomerNotes: "+WS_CustomerNotes+Char(13)

WSRECORDLOG ($String)

READ ONLY([Customer])
UNLOAD RECORD([Customer])
ReadOnlyAll 
UnloadAll 