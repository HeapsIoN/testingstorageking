//%attributes = {"publishedSoap":true,"publishedWsdl":true,"lang":"en"} comment added and reserved by 4D.

// ----------------------------------------------------
// User name (OS): robert.richardson
// Date and time: 08/04/11, 17:07:00
// API Version: 1.0
// ----------------------------------------------------
// Method: WS_GeneratePIN
// Description
// Generates a random PIN for a facility
//
// Parameters
// ----------------------------------------------------

SOAP DECLARATION(WS_FacilityID;Is text;SOAP input)
SOAP DECLARATION(WS_WebServicePassword;Is text;SOAP input)


SOAP DECLARATION(WS_NewPIN;Is text;SOAP output)
SOAP DECLARATION(WS_ORSuccess;Is Boolean;SOAP output)
SOAP DECLARATION(WS_ORErrorCode;Is integer;SOAP output)
SOAP DECLARATION(WS_ORErrorDescription;Is string var;SOAP output)

$String:="Start WS_GeneratePIN"+Char(13)
$String:=$String+"WS_FacilityID: "+WS_FacilityID+Char(13)
C_TEXT($pass)
$pass:=WS_WebServicePassword
$pass:=Replace string($pass;Substring($pass;1;Length($pass)-2);"X"*(Length($pass)-2))
$String:=$String+"WS_WebServicePassword: "+$pass+Char(13)
WSRecordLog ($String)
TRACE

QUERY([Facility];[Facility]FacilityCode=WS_FacilityID)

If (Records in selection([Facility])=1)
If (WS_WebServicePassword=[Facility]ODBCPassword) & ([Facility]ODBCPassword#"")

WS_NewPIN:=RandomPINNew 
WS_ORSuccess:=True
WS_ORErrorCode:=0
WS_ORErrorDescription:=""

Else 
WS_ORSuccess:=False
WS_ORErrorCode:=1099
WS_ORErrorDescription:="Web Service Password is incorrect."
End if 
Else 
WS_ORSuccess:=False
WS_ORErrorCode:=1001
WS_ORErrorDescription:="Facility record not found in database Storman."
End if 

$String:="End WS_GeneratePIN"+Char(13)
$String:=$String+"WS_NewPIN: "+WS_NewPIN+Char(13)
$String:=$String+"WS_ORSuccess: "+String(WS_ORSuccess)+Char(13)
$String:=$String+"WS_ORErrorCode: "+String(WS_ORErrorCode)+Char(13)
$String:=$String+"WS_ORErrorDescription: "+WS_ORErrorDescription+Char(13)
WSRecordLog ($String)


ReadOnlyAll 
UnloadAll 
