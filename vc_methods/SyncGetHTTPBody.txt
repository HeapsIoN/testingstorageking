//%attributes = {"lang":"en"} comment added and reserved by 4D.
//SDD 9/2/11 Added ;* to Position commands
//SDD 25/2/11 Still getting errors on the COPY BLOB line so it could be packets > 32000 - writing it to disk for checking and not allowing invalid value for $vliOffset
//SDD 25/2/11 Also changed from 3 to UTF8 text without length

C_POINTER($1;$2)//$1 is arriving Blob containing both header and body, $2 contains resulting body
C_LONGINT($vliCRPos;$vliOffset)
C_TEXT($0)
If (BLOB size($1->)>20)//Dont do this if the Blob is small or blank
$vliOffset:=0
$cr:=Char(13)+Char(10)

$vtText:=BLOB to text($1->;Mac text without length;$vliOffset;32000)
$vliCRPos:=Position($cr+$cr;$vtText;*)
$vliCRPos:=$vliCRPos+4

While (Substring($vtText;$vliCRPos;5)="HTTP/")
$vliOffset:=$vliCRPos-1
$vtText:=BLOB to text($1->;Mac text without length;$vliOffset;32000)
$vliCRPos:=Position($cr+$cr;$vtText;*)
$vliCRPos:=$vliCRPos+4
End while 

$vliOffset:=$vliCRPos-1
If ($vliOffset<0) | ($vliOffset>BLOB size($1->))//Second condition is theoretically impossible but trying to prevent error
$vliOffset:=0
BLOB TO DOCUMENT("BlobInERROR"+Replace string(String(Current time);":";"")+".txt";$1->)
End if 
If ($vliOffset<BLOB size($1->))
COPY BLOB($1->;$2->;$vliOffset;0;BLOB size($1->)-$vliOffset)
End if 
Else 
BLOB TO DOCUMENT("BlobInERROR"+Replace string(String(Current time);":";"")+".txt";$1->)
End if 