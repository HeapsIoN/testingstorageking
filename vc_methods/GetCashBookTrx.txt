//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_TEXT($1)//[ChartAccts]AcctType Code to Find
C_TEXT($2)//Specific Account number
If ([Journal]JrnlType="REC") & ($1#"B@") & ($1#"T")
QUERY([CashBookTrxs];[CashBookTrxs]TrxLinkNo=[CashBook]TrxLinkNo)
Else 
QUERY([CashBookTrxs];[CashBookTrxs]JournalNo=[Journal]JournalNo)
End if 
If ($1="")
QUERY([ChartAccts];[ChartAccts]AcctNo=$2)
QUERY SELECTION([CashBookTrxs];[CashBookTrxs]AcctNo=$2)
Else 
QUERY([ChartAccts];[ChartAccts]AcctType=$1)
QUERY SELECTION([CashBookTrxs];[CashBookTrxs]AcctNo=[ChartAccts]AcctNo)
End if 
If (Records in selection([CashBookTrxs])=0)
CREATE RECORD([CashBookTrxs])
[CashBookTrxs]AcctNo:=[ChartAccts]AcctNo
[CashBookTrxs]JournalNo:=[Journal]JournalNo
If ([Journal]JrnlType="REC") & ($1#"B@") & ($1#"T")
[CashBookTrxs]TrxLinkNo:=[CashBook]TrxLinkNo
End if 
[CashBookTrxs]SeqNo:=Sequence number([CashBookTrxs])
Case of 
: ([Journal]JrnlType="INC") | ([Journal]JrnlType="EXP") | ([Journal]JrnlType="PMT") | ([Journal]JrnlType="PRE")
[CashBookTrxs]TrxDate:=GetPeriodEndDate ([Journal]PeriodNo)
: ([Journal]JrnlType="REC") & ($1="B@")//Use period end date for Bank account
[CashBookTrxs]TrxDate:=GetPeriodEndDate ([Journal]PeriodNo)
: ([Journal]JrnlType="RPR")
[CashBookTrxs]TrxDate:=GetPeriodEndDate ([Journal]PeriodNo-1)+1
Else 
[CashBookTrxs]TrxDate:=[CashBook]InvDate
End case 
SAVE RECORD([CashBookTrxs])
End if 