//%attributes = {"publishedSoap":true,"publishedWsdl":true,"lang":"en"} comment added and reserved by 4D.

// ----------------------------------------------------
// User name (OS): robert.richardson
// Date and time: 07/03/11, 05:57:00
// ----------------------------------------------------
// Method: WS_GetCustCodes
// Description
// Returns customer codes across all facilities for a given email address
//
// Parameters
// ----------------------------------------------------

SOAP DECLARATION(WS_WebID;Is text;SOAP input)
SOAP DECLARATION(WS_WebServicePassword;Is text;SOAP input)

SOAP DECLARATION(WS_atCustCodes;Text array;SOAP output)
//WS_OLD=SOAP DECLARATION(WS_Edit;Is Boolean;SOAP Output)
//WS_OLD=SOAP DECLARATION(WS_WebIDOut;Is Text;SOAP Output)

SOAP DECLARATION(WS_ORSuccess;Is Boolean;SOAP output)
SOAP DECLARATION(WS_ORErrorCode;Is integer;SOAP output)
SOAP DECLARATION(WS_ORErrorDescription;Is string var;SOAP output)

$String:="Start WS_GetCustCodes"+Char(13)
$String:=$String+"WS_WebID: "+WS_WebID+Char(13)
C_TEXT($pass)
$pass:=WS_WebServicePassword
$pass:=Replace string($pass;Substring($pass;1;Length($pass)-2);"X"*(Length($pass)-2))
$String:=$String+"WS_WebServicePassword: "+$pass+Char(13)
WSRecordLog ($String)


//WS_OLD=WS_WebIDOut:=WS_WebID

QUERY([Customer];[Customer]WebID=WS_WebID)
QUERY([Facility];[Facility]FacilityCode=Substring([Customer]CustomerCode;1;5))

If (WS_WebServicePassword=[Facility]ODBCPassword) & ([Facility]ODBCPassword#"")

If (Records in selection([Customer])>0)
//WS_OLD=WS_Edit:=True
SELECTION TO ARRAY([Customer]CustomerCode;WS_atCustCodes)
WS_ORSuccess:=True
WS_ORErrorCode:=0
WS_ORErrorDescription:=""
//WS_ORErrorDescription:="Customer Codes successfully retrieved."
Else 
//WS_OLD=WS_Edit:=False
WS_ORSuccess:=True
WS_ORErrorCode:=1012
WS_ORErrorDescription:="Customer record/s not found in database Storman."
End if 
Else 
//WS_OLD=WS_Edit:=False
WS_ORSuccess:=False
WS_ORErrorCode:=1099
WS_ORErrorDescription:="Web Service Password is incorrect."
End if 

$String:="End WS_GetCustCodes"+Char(13)
$sizeofarray:=Size of array(WS_atCustCodes)
For ($vlElem;1;$sizeofarray)
$String:=$String+"WS_atCustCodes("+String($vlElem)+"):"+WS_atCustCodes{$vlElem}+Char(13)
End for 
$String:=$String+"WS_ORSuccess: "+String(WS_ORSuccess)+Char(13)
$String:=$String+"WS_ORErrorCode: "+String(WS_ORErrorCode)+Char(13)
$String:=$String+"WS_ORErrorDescription: "+WS_ORErrorDescription+Char(13)
WSRecordLog ($String)


ReadOnlyAll 
UnloadAll 
