//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_LONGINT($0;$i)//Error passed back
C_TEXT($1)//AnalysisCode
C_DATE($2)//TrxDate
C_REAL($3;$4;$5)//TaxAmt & TotAmt & DiscAmt
C_BOOLEAN($6;$7)//[Transaction]Charge
//$7 True if Saving record / False in Undoing
C_POINTER($p)
$0:=0
If ($2=!00-00-00!)
$0:=-16020
Else 
If ($6)
If ([Trxs]Match>-50)//Don't include Stock Purchases or Bank Trxs SD 1/9/05
$i:=Find in array(<>asCharges;$1)
If ($i<1) | ($i>Size of array(<>asCharges))
$p:=->[Activity]OtherChg
Else 
Case of 
: (<>asChargeCat{$i}="Rent") | (<>asChargeCat{$i}="Upg & Support")
$p:=->[Activity]RentChg
: (<>asChargeCat{$i}="Deposit") | (<>asChargeCat{$i}="Licences")
$p:=->[Activity]DepositChg
: (<>asChargeCat{$i}="Late Fees") | (<>asChargeCat{$i}="MV Hosting")
$p:=->[Activity]LateFeeChg
: (<>asChargeCat{$i}="Insurance") | (<>asChargeCat{$i}="Internet Trxs")
$p:=->[Activity]InsureChg
: (<>asChargeCat{$i}="Merchandise") | (<>asChargeCat{$i}="Access Control")
$p:=->[Activity]MerchChg
If (<>sFacility="KE@") & ([Trxs]TotAmt<0) & ([Trxs]AnalysisCode#(<>sFacility+"MD")) & ([Trxs]AgreeNo#<>sFacility) & ([Trxs]Charge=True)
LogActivity (Substring([Trxs]AgreeNo;1;5);$2;->[Activity]MerchReturnChg;Round($4-$3;2);->[Activity]TaxChg;$3;->[Activity]DiscChg;$5)
End if 
: (<>asChargeCat{$i}="Bad Debts")
$p:=->[Activity]BadDebtChg
: (<>asChargeCat{$i}="Discounts")
$p:=->[Activity]DiscChg
Else 
$p:=->[Activity]OtherChg
End case 
End if 
$0:=LogActivity (Substring([Trxs]AgreeNo;1;5);$2;$p;Round($4-$3;2);->[Activity]TaxChg;$3;->[Activity]DiscChg;$5)
If ($0=0)
Case of 
: (Trigger event=On Saving New Record Event) & ([Trxs]AnalysisCode="@LF") & ([Trxs]TotAmt<0)
$0:=LogActivity (Substring([Trxs]AgreeNo;1;5);[Trxs]TrxDate;->[Activity]LateFeeWaived;[Trxs]TotAmt*-1)
: (Trigger event=On Deleting Record Event) & (Old([Trxs]AnalysisCode)="@LF")
$0:=LogActivity (Substring([Trxs]AgreeNo;1;5);Old([Trxs]TrxDate);->[Activity]LateFeeWaived;Old([Trxs]TotAmt))
Else 
//Do Nothing
End case 
End if 
End if 
Else 
If ([Analysis]Code#$1)
QUERY([Analysis];[Analysis]Code=$1)
End if 
If ([Analysis]SortOrder<95) & ([Trxs]Match>-50)
Case of 
: (($4<0) & ($7)) | (($4>0) & ($7=False))
$p:=->[Activity]Refunds
: ([Analysis]InclBankRpt=False)
$p:=->[Activity]CreditRec
Else 
$p:=->[Activity]CashRec
End case 
$0:=LogActivity (Substring([Trxs]AnalysisCode;1;5);$2;$p;Round($4;2))
End if 
End if 
End if 