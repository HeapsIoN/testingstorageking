//%attributes = {"lang":"en"} comment added and reserved by 4D.

// ----------------------------------------------------
// User name (OS): robert.richardson
// Date and time: 01/08/11, 10:28:54
// ----------------------------------------------------
// Method: DockWalkReport
// Description
// Generates and prints the dock walk report for Marinas
//
// Parameters
// ----------------------------------------------------

If (PermissionsControl (66))
SetFacility 

vtTitle:="Dock Walk Report"
vLegend:=""

DEFAULT TABLE([Unit])

ARRAY TEXT(asSigns;Size of array(<>asUnitStatus))
For ($i;1;Size of array(asSigns))
If (asSigns{$i}#"•")
asSigns{$i}:=""
End if 
End for 
$Window:=Open window((Screen width/2)-270;(Screen height/2)-272;(Screen width/2)+270;(Screen height/2)+272;5;vtTitle)
DIALOG([Unit];"UnitStatusDialog")
CLOSE WINDOW($Window)

If (OK=1)
Case of 
: (bAll=1)
QUERY([Unit];[Unit]RUnitNo=<>sFacility+"@")
: (bCode=1)
QUERY([Unit];[Unit]RUnitNo=<>sFacility+"@";*)
QUERY([Unit]; & ;[Unit]StatusChange=True)
$Clear:=True
: (bAdd=1)
$First:=True
For ($i;1;Size of array(<>asUnitStatus))
If (asSigns{$i}="•")
If ($First)
$First:=False
QUERY([Unit];[Unit]Status=<>asUnitStatus{$i};*)
If (<>asUnitStatus{$i}="Reserved")
QUERY([Unit]; | ;[Unit]ReservedTo#"";*)
End if 
Else 
QUERY([Unit]; | ;[Unit]Status=<>asUnitStatus{$i};*)
End if 
End if 
End for 
QUERY([Unit]; & ;[Unit]RUnitNo=<>sFacility+"@")
$First:=True
For ($i;1;Size of array(atLocked))
If (asSigns1{$i}="•")
If ($First)
$First:=False
Case of 
: ($i=1)
QUERY SELECTION([Unit];[Unit]LockStatus=2;*)
QUERY SELECTION([Unit]; | ;[Unit]LockStatus=3;*)
: ($i=2)
QUERY SELECTION([Unit];[Unit]LockStatus=4;*)
QUERY SELECTION([Unit]; | ;[Unit]LockStatus=5;*)
: ($i=3)
QUERY SELECTION([Unit];[Unit]LockStatus=6;*)
Else 
//Do Nothing
End case 
Else 
Case of 
: ($i=1)
QUERY SELECTION([Unit]; | ;[Unit]LockStatus=2;*)
QUERY SELECTION([Unit]; | ;[Unit]LockStatus=3;*)
: ($i=2)
QUERY SELECTION([Unit]; | ;[Unit]LockStatus=4;*)
QUERY SELECTION([Unit]; | ;[Unit]LockStatus=5;*)
: ($i=3)
QUERY SELECTION([Unit]; | ;[Unit]LockStatus=6;*)
Else 
//Do Nothing
End case 
End if 
End if 
End for 
QUERY SELECTION([Unit]; & ;[Unit]RUnitNo=<>sFacility+"@")
Else 
QUERY([Unit];[Unit]RUnitNo=<>sFacility+"@")
End case 
Case of 
: (rRefund1=1)
SET AUTOMATIC RELATIONS(True;False)
ORDER BY([Unit];[UnitType]SortOrder;>;[Unit]UnitTypeCode;>;[Unit]UnitNo;>)
SET AUTOMATIC RELATIONS(False;False)
: (rRefund2=1)
ORDER BY([Unit];[Unit]UnitNo;>)
: (rRefund4=1)
ORDER BY([Unit];[Unit]WalkOrder;>)
Else 
ORDER BY([Unit];[Unit]Status;>;[Unit]UnitNo;>)
End case 
FORM SET OUTPUT([Unit];"DockWalk")
FIRST RECORD([Unit])
PrintSelection ("L")
FORM SET OUTPUT([Unit];"UnitOut")
End if 
End if 