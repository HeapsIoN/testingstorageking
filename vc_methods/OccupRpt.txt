//%attributes = {"lang":"en"} comment added and reserved by 4D.

If (Form event=On Load)
C_LONGINT($i)
SetFormats 
If (vMthDate#!00-00-00!) & ((vReportSettings[[11]]="Y") | (<>tBatch))
QUERY([Unit];[Unit]UnitTypeCode=[UnitType]RUnitType;*)
QUERY([Unit]; & ;[Unit]StartDate<=vMthDate)
Else 
QUERY([Unit];[Unit]UnitTypeCode=[UnitType]RUnitType)
End if 
//QUERY([Unit]; & ;[Unit]Status#"Unavail@")
CREATE EMPTY SET([Unit];"toprint")
For ($i;1;Records in selection([Unit]))
If ([Unit]Status="Unavail@")
QUERY([UnitHistory];[UnitHistory]MoveOut>vMthDate;*)
QUERY([UnitHistory]; | ;[UnitHistory]MoveOut=!00-00-00!;*)
QUERY([UnitHistory]; & ;[UnitHistory]Casual=False;*)
QUERY([UnitHistory]; & ;[UnitHistory]MoveIn<=vMthDate;*)
QUERY([UnitHistory]; & ;[UnitHistory]UnitNo=[Unit]RUnitNo)
If (Records in selection([UnitHistory])>0)
ADD TO SET([Unit];"toprint")
End if 
Else 
ADD TO SET([Unit];"toprint")
End if 
NEXT RECORD([Unit])
End for 
USE SET("toprint")
CLEAR SET("toprint")
[UnitType]NoTotal:=Records in selection([Unit])
[UnitType]AreaSizeTotal:=0
vOccUnit:=0
[UnitType]AreaSizeOccup:=0
[UnitType]RentedValue:=0



For ($i;1;Records in selection([Unit]))
[UnitType]AreaSizeTotal:=Round([UnitType]AreaSizeTotal+[Unit]UnitAreaSize;2)
If ((tBoolean) & ([Unit]OccupRentedVal#-999)) | ((tBoolean=False) & ([Unit]Vacant=False))
vOccUnit:=vOccUnit+1
[UnitType]AreaSizeOccup:=Round([UnitType]AreaSizeOccup+[Unit]UnitAreaSize;2)
If (tBoolean)
[UnitType]RentedValue:=[UnitType]RentedValue+[Unit]OccupRentedVal
Else 
[UnitType]RentedValue:=[UnitType]RentedValue+[Unit]UnitValue
End if 
End if 
NEXT RECORD([Unit])
End for 
[UnitType]NoVacant:=[UnitType]NoTotal-vOccUnit



If ([UnitType]VariableLength)
vUnitBaseRate:=[UnitType]UnitTypeLength*[UnitType]MonthlyRate
Else 
vUnitBaseRate:=[UnitType]MonthlyRate
End if 
If ([Facility]TaxAddtoAmt)
[UnitType]RentedValue:=Round([UnitType]RentedValue*(1+(vTax/100));2)
vRate:=Round(vUnitBaseRate*(1+(vTax/100));2)
Else //Documentation
[UnitType]RentedValue:=Round([UnitType]RentedValue/(1+(vTax/100));2)
vRate:=Round(vUnitBaseRate/(1+(vTax/100));2)
End if 
vTotal:=vRate*[UnitType]NoTotal
If (vReportSettings[[4]]="2")
[UnitType]RentedValue:=Round(vOccUnit*vRate;2)
End if 
vVacVal:=Round([UnitType]NoVacant*vRate;2)
vVacArea:=Round([UnitType]AreaSizeTotal-[UnitType]AreaSizeOccup;2)
If ([UnitType]NoTotal=0)
vPCUOcc:=0
vPCUVac:=0
Else 
vPCUOcc:=Round(vOccUnit*100/[UnitType]NoTotal;1)
vPCUVac:=Round([UnitType]NoVacant*100/[UnitType]NoTotal;1)
End if 
If (vTotal=0)
vPCVOcc:=0
vPCVVac:=0
Else 
vPCVOcc:=Round([UnitType]RentedValue*100/vTotal;1)
vPCVVac:=Round(vVacVal*100/vTotal;1)
End if 
If ([UnitType]AreaSizeTotal=0)
vPCAOcc:=0
vPCAVac:=0
Else 
vPCAOcc:=Round([UnitType]AreaSizeOccup*100/[UnitType]AreaSizeTotal;1)
vPCAVac:=Round(100-vPCAOcc;1)
End if 
End if 
If (Form event=On Header)
If (tBoolean)
vTitle:=Get localized string("Occupancy Report as at")+" "+String(vMthDate;6)
Else 
vTitle:=Get localized string("Occupancy Report for")+" "+String(Current date;6)
End if 
Case of 
: (<>sProduct="@R")
vsUn:=Get localized string("OccupRptMethod")
vsUns:=Get localized string("OccupRptMethod1")
: (<>sProduct="@B")
vsUn:=Get localized string("Berth")
vsUns:=Get localized string("Berths")
: (<>sProduct="@C")
vsUn:=Get localized string("Space")
vsUns:=Get localized string("Spaces")
Else 
vsUn:=Get localized string("Unit")
vsUns:=Get localized string("Units")
End case 

If (BLOB size([Facility]ReportLogo_)#0)
LoadReferences (4)
End if 
End if 
If (Form event=On Printing Break)
vNoTotal:=Subtotal([UnitType]NoTotal)
vTotal:=Subtotal(vTotal)
vAvgRate:=Round(vTotal/vNoTotal;2)
vNoOccup:=Subtotal(vOccUnit)
vNoVacant:=Subtotal([UnitType]NoVacant)
vOccVal:=Subtotal([UnitType]RentedValue)
vVacVal:=Subtotal(vVacVal)
vTotArea:=Subtotal([UnitType]AreaSizeTotal)
vOccArea:=Subtotal([UnitType]AreaSizeOccup)
vVacArea:=Subtotal(vVacArea)
If (vNoTotal=0)
vPCUOcc:=0
vPCUVac:=0
Else 
vPCUOcc:=Round(vNoOccup*100/vNoTotal;1)
vPCUVac:=Round(vNoVacant*100/vNoTotal;1)
End if 
If (vTotal=0)
vPCVOcc:=0
vPCVVac:=0
Else 
vPCVOcc:=Round(vOccVal*100/vTotal;1)
vPCVVac:=Round(vVacVal*100/vTotal;1)
End if 
If (vTotArea=0)
vPCAOcc:=0
vPCAVac:=0
Else 
vPCAOcc:=Round(vOccArea*100/vTotArea;1)
vPCAVac:=Round(vVacArea*100/vTotArea;1)
End if 
OccupGetLegend 
End if 
If (Form event=On Printing Footer)
SetFooter 
End if 

If (Form event=On Printing Detail)
OBJECT SET VISIBLE(*;"SpecialStar";[UnitType]RCSpecial)
End if 