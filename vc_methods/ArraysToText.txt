//%attributes = {"lang":"en"} comment added and reserved by 4D.
//SDD Routine to pack arrays inside of a text field  //TextToArrays does the opposite
//Array Type of first array -1=Integer, -2=Longint, -3=Real, -4=Boolean, -5=Date, -6=Time, +'ve=String of that length
//Calling syntax  $Text:=ArraysToText (->aLength;->aAnalysis;->aDesc;->aDate;->aQty;->aAmt)
C_TEXT($0;$Text)//Contains the contents of the arrays at the end - N.B. All arrays must contain the same number of elements except the first one
C_POINTER(${1})
C_TEXT($t;$cr)
C_LONGINT($i;$j;$x)
C_POINTER($p)
$x:=Size of array(${2}->)
$t:=Char(16)//SyncCode uses ASCII 18,19,20 so this uses 16 and 17
$cr:=Char(17)
$Text:=String(Count parameters-1)+$t+String($x)+$cr
For ($i;1;$x)
For ($j;2;Count parameters)
$p:=${$j}
If (Size of array($p->)=$x)//SDD This to prevent syntax error if arrays are not the same size
Case of 
: (${1}->{$j-1}=-4)//Boolean
If ($p->{$i}=True)
$Text:=$Text+"T"+$t
Else 
$Text:=$Text+"F"+$t
End if 
: (${1}->{$j-1}=-5)//Date format YYYY-MM-DD
$Text:=$Text+Substring(String($p->{$i};ISO date);1;10)+$t
: (${1}->{$j-1}<0)
$Text:=$Text+String(Round($p->{$i};2))+$t//Number
Else 
$Text:=$Text+$p->{$i}+$t//String
End case 
End if 
End for 
$Text:=$Text+$cr
End for 
$0:=$Text