//%attributes = {"publishedWeb":true,"lang":"en"} comment added and reserved by 4D.
C_TEXT($0;$1;$In;$Temp)//$1=field to swap around
C_LONGINT($2;$Action;$Pos)//Value of $2 determines what action to take place
//1=Swap name if comma (e.g. Smith, Mr John -> Mr John Smith)
//2=Find firstname automatically (e.g. Smith, Mr John -> John)
//3=Swap name without firstname if comma (e.g. Smith, Mr John -> Mr Smith)
//4=Upper to Lowercase (e.g. SMITH, MR JOHN -> Smith, Mr John)
If (Count parameters>1)
$Action:=$2
Else 
$Action:=1
End if 
$In:=$1
If (Length($In)>0)
Case of 
: ($Action=1)
$Pos:=Position(",";$In)
If ($Pos<1)
$0:=$In
Else 
$In:=Substring($In;$Pos+1)+" "+Substring($In;1;$Pos-1)
$Pos3:=Position(" ";$In)
If ($Pos3=1)
Repeat 
$In:=Delete string($In;1;1)
$Pos3:=Position(" ";$In)
Until ($Pos3#1)
End if 
$0:=$In
End if 
: ($Action=3)
$Pos:=Position(",";$In)
If ($Pos<1)
$0:=""
Else 
$Temp:=Substring($In;$Pos+1)
$Pos3:=Position(" ";$Temp)
If ($Pos3=1)
Repeat 
$Temp:=Delete string($Temp;1;1)
$Pos3:=Position(" ";$Temp)
Until ($Pos3#1)
End if 
$Pos2:=Position(" ";$Temp)
$0:=Substring($Temp;1;$Pos2)+Substring($In;1;$Pos-1)
End if 
: ($Action=2)
$Pos:=Position(",";$In)
If ($Pos<1)
$0:=""
Else 
$Temp:=Substring($In;$Pos+1)
$Pos3:=Position(" ";$Temp)
If ($Pos3=1)
Repeat 
$Temp:=Delete string($Temp;1;1)
$Pos3:=Position(" ";$Temp)
Until ($Pos3#1)
End if 
If ($Temp="Mr@") | ($Temp="Mrs@") | ($Temp="Miss@") | ($Temp="Ms@") | ($Temp="Dr@")
$Pos2:=Position(" ";$Temp)
$Temp:=Substring($Temp;$Pos2+1)
End if 
$Pos2:=Position(" ";$Temp)
If ($Pos2<1)
$0:=$Temp
Else 
$0:=Substring($Temp;1;$Pos2-1)
End if 
End if 
: ($Action=4)
If (Character code(Substring($In;2;1))<97) | (Character code(Substring($In;4;1))<97)
$In:=Lowercase($In)
$In[[1]]:=Uppercase($In[[1]])
For ($x;2;Length($In)-1)
If ($In[[$x]]<"A")
$In[[$x+1]]:=Uppercase($In[[$x+1]])
End if 
End for 
If ($In="mc@")
$In[[3]]:=Uppercase($In[[3]])
End if 
If ($In="mac@")
$In[[4]]:=Uppercase($In[[4]])
End if 
End if 
$0:=$In
Else 
//Do Nothing
End case 
If (Length($0)>Length($1))
AlertMessage (Get localized string("Input")+": "+$1+" "+Get localized string("Type")+": "+String($2)+" "+Get localized string("Output")+": "+$0)
End if 
Else 
$0:=""
End if 