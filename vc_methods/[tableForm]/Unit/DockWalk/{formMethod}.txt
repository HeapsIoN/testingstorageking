//%attributes = {"lang":"en"} comment added and reserved by 4D.
Case of 
: (Form event=On Load)
C_TEXT(vtLiveOnBoard)
ARRAY LONGINT(aiLength;5)
aiLength{1}:=25
aiLength{2}:=25
aiLength{3}:=25
aiLength{4}:=-2
aiLength{5}:=-2

: (Form event=On Header)
OBJECT SET TITLE(*;"Text4";Get localized string("Berth No"))

If (BLOB size([Facility]ReportLogo_)#0)
LoadReferences (4)
End if 
: (Form event=On Printing Detail)
RELATE MANY([Unit]RUnitNo)
RELATE ONE([Unit]AgreeNo)
RELATE ONE([Agreement]CustomerCode)

Case of 
: ([Customer]PrimaryPhone=2)
vPhone:=[Customer]PhoneBus
: ([Customer]PrimaryPhone=3)
vPhone:=[Customer]MobilePhone
: ([Customer]PrimaryPhone=4)
vPhone:=[Customer]MilCPhone
Else 
vPhone:=[Customer]PhonePrivate
End case 

$LiveonBoard:=<>sFacility+"LOB"

QUERY([OtherChgs];[OtherChgs]AgreeNo=[Agreement]RAgreeNo;*)
QUERY([OtherChgs]; & ;[OtherChgs]AnalysisCode=$LiveonBoard)

If (Records in selection([OtherChgs])>0)
vtLiveOnBoard:="True"
Else 
vtLiveOnBoard:="False"
End if 

ARRAY TEXT(asEngineSerialNo;0)
ARRAY TEXT(asEngineType;0)
ARRAY TEXT(asEngineBrand;0)
ARRAY LONGINT(asEngineHP;0)
ARRAY LONGINT(asEngineYear;0)

TextToArrays (->[Boat]Engine;->aiLength;->asEngineSerialNo;->asEngineType;->asEngineBrand;->asEngineHP;->asEngineYear)

If (Size of array(asEngineType)>0)
vtEngine:=asEngineType{1}
Else 
vtEngine:=""
End if 

If (Records in selection([Boat])=0)
OBJECT SET VISIBLE([Boat]Length;False)
Else 
OBJECT SET VISIBLE([Boat]Length;True)
End if 

: (Form event=On Printing Footer)
SetFooter 

ARRAY TEXT(asEngineSerialNo;0)
ARRAY TEXT(asEngineType;0)
ARRAY TEXT(asEngineBrand;0)
ARRAY LONGINT(asEngineHP;0)
ARRAY LONGINT(asEngineYear;0)
ARRAY LONGINT(aiLength;0)
End case 