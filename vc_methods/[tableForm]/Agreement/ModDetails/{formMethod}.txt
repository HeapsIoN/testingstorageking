//%attributes = {"lang":"en"} comment added and reserved by 4D.
If (Form event=On Load)

If (vTitle=Get localized string("Delete Agreements"))
FORM GOTO PAGE(2)
End if 

C_TEXT(vPrinted;vsAutoPayType)
OBJECT SET TITLE(*;"Text1";Get localized string("Agreement Number")+":")
OBJECT SET TITLE(*;"Text7";Get localized string("Customer Name")+":")
OBJECT SET TITLE(*;"Text13";Get localized string("Customer Code")+":")
Case of 
: (<>sProduct="@B")
OBJECT SET TITLE(*;"Text15";Get localized string("Customer Berths"))
: (<>sProduct="@R")
OBJECT SET TITLE(*;"Text15";Get localized string("Customer Appliances"))
: (<>sProduct="@C")
OBJECT SET TITLE(*;"Text15";Get localized string("Customer Spaces"))
Else 
OBJECT SET TITLE(*;"Text15";Get localized string("Customer Units"))
End case 

OBJECT SET TITLE(*;"Text2";Get localized string("Last Bill Date")+":")
OBJECT SET TITLE(*;"Text8";Get localized string("Amount to bill")+":")
OBJECT SET TITLE(*;"Text19";Get localized string("Billing Plan")+":")
OBJECT SET TITLE(*;"Text17";Get localized string("Day to Bill")+":")
OBJECT SET TITLE(*;"Text4";Get localized string("AutoPay customer")+":")
OBJECT SET TITLE(*;"Text18";Get localized string("Type")+":")
OBJECT SET TITLE(*;"Text21";Get localized string("Authorised amount")+":")
OBJECT SET TITLE(*;"Text20";Get localized string("Plan No. & Notice")+":")

SetFormats 
RELATE ONE([Agreement]CustomerCode)
If (PermissionsControl (150;False)=False)
OBJECT SET ENTERABLE([Agreement]NoticeAmtToBill;False)
OBJECT SET ENTERABLE([Agreement]LateCycleStDate;False)
OBJECT SET ENTERABLE([Agreement]SendNotices;False)
End if 
If (PermissionsControl (32;False)=False)
OBJECT SET ENTERABLE([Agreement]LateCycleStDate;False)
End if 
CREATE SET([Notices];"DailyRestore")
QUERY([Notices];[Notices]NoticePlanNo=[Agreement]NoticePlanNo;*)
QUERY([Notices]; & ;[Notices]Description=[Agreement]NoticeBilled)
Case of 
: ([Agreement]NoticeBilled="AARecurring Rent")
vPrinted:=Get localized string("Processed for Autopay")
: ([Agreement]SendNoticesEmail=True) & ([Agreement]SendNoticeSMS=True) & ([Notices]LetterOnly=False) & ([Notices]SMSMessage>"")
vPrinted:=Get localized string("Emailed/Texted")
: ([Agreement]SendNoticesEmail=True) & ([Notices]LetterOnly=False)
vPrinted:=Get localized string("Emailed")
: ([Agreement]SendNoticeSMS=True) & ([Notices]SMSMessage>"")
vPrinted:=Get localized string("Printed/Texted")
Else 
vPrinted:=Get localized string("Printed")
End case 
USE SET("DailyRestore")
CLEAR SET("DailyRestore")
Case of 
: ([Agreement]AutoPayType=1)
vsAutoPayType:=Get localized string("Bank Authority")
: ([Agreement]AutoPayType=2)
vsAutoPayType:=Get localized string("Auto Credit Card")
: ([Agreement]AutoPayType=3)
vsAutoPayType:=Get localized string("Auto Direct Debit")
: ([Agreement]AutoPayType=4)
vsAutoPayType:=Get localized string("Direct Debit")
Else 
vsAutoPayType:=Get localized string("None")
End case 
End if 