//%attributes = {"lang":"en"} comment added and reserved by 4D.
If (vNoteHistBody>"")
CREATE RECORD([NotesHistory])
[NotesHistory]CustomerCode:=Substring([Contacts]ContactNo;1;5)
[NotesHistory]ContactNo:=[Contacts]ContactNo
[NotesHistory]NoteDate:=Current date
[NotesHistory]NoteTime:=Current time
If (asNotesList>0)
If (asNotesList{asNotesList}#"<None>")
[NotesHistory]Category:=asNotesList{asNotesList}
End if 
End if 
If (Length(vNoteHistBody)>20000)
AlertMessage (Get localized string("Your note is longer than 20000 characters and will be truncated."))
[NotesHistory]Note:=Substring(vNoteHistBody;1;20000)
Else 
[NotesHistory]Note:=vNoteHistBody
End if 
[NotesHistory]UserLogin:=<>sCurrentUser
QUERY([MarketingTypes];[MarketingTypes]Facility=Substring([NotesHistory]CustomerCode;1;5);*)
QUERY([MarketingTypes]; & ;[MarketingTypes]Description=[NotesHistory]Category)
If ([MarketingTypes]Security=True)
[Contacts]ThreatLevel:=[Contacts]ThreatLevel+1

If ([Contacts]ThreatLevel>0)
vThreatLevel:="Threat Level: "+String([Contacts]ThreatLevel)
If ([Contacts]ThreatLevel>=[Facility]ThreatLevelAlert)
OBJECT SET RGB COLORS(vThreatLevel;0x00FF0000;0x00FF0000)
Else 
OBJECT SET RGB COLORS(vThreatLevel;0x00FFA500;0x00FFA500)
End if 
Else 
vThreatLevel:=""
End if 

[Contacts]ThreatLevelDate:=[NotesHistory]NoteDate
[NotesHistory]Security:=True
If ([Contacts]ThreatLevel>=[Facility]ThreatLevelAlert)
AlertMessage (Get localized string("This contact has exceeded the company threat level."))
End if 
End if 
SAVE RECORD([NotesHistory])
vNoteHistBody:=""
Else 
AlertMessage (Get localized string("Type a new note before clicking on Add button!"))
End if 
QUERY([NotesHistory];[NotesHistory]ContactNo=[Contacts]ContactNo)
If (tClear=False)
ORDER BY([NotesHistory];[NotesHistory]NoteDate;<;[NotesHistory]NoteTime;<)
Else 
ORDER BY([NotesHistory];[NotesHistory]NoteDate;>;[NotesHistory]NoteTime;>)
End if 