//%attributes = {"lang":"en"} comment added and reserved by 4D.
//Copy codes to other sites
C_LONGINT($i;$j)
C_TEXT($FromSite)
USE SET("ListboxSet1")
QUERY([MerchPlan];[MerchPlan]MerchPlanID=vFacilityCode+"@";*)
QUERY([MerchPlan]; & [MerchPlan]Inactive=False)
If (Records in selection([MerchPlan])>0)
$FromSite:=vFacilityCode
ARRAY TEXT(asMerchPlan;0)
SELECTION TO ARRAY([MerchPlan]MerchPlanID;asMerchPlan)
OK:=ConfirmThree (Get localized string("Duplicate")+" "+String(Records in selection([MerchPlan]))+" "+Get localized string("Merchandise Discount Plans")+" "+Get localized string("to1")+" "+String(Records in selection([Facility]))+" other facilities?";Get localized string("Yes");Get localized string("Select Sites");Get localized string("No"))
If (OK=1)
ARRAY TEXT(asFields;Records in selection([Facility]))
ARRAY TEXT(asSiteCodes;Records in selection([Facility]))
ARRAY TEXT(asSigns;Records in selection([Facility]))
For ($i;1;Records in selection([Facility]))
asFields{$i}:=[Facility]FacilityName
asSiteCodes{$i}:=[Facility]FacilityCode
asSigns{$i}:=""
NEXT RECORD([Facility])
End for 
End if 
If (OK=2)
ARRAY TEXT(asFields;Size of array(<>asSiteCodes))
ARRAY TEXT(asSiteCodes;Size of array(<>asSiteCodes))
ARRAY TEXT(asSigns;Size of array(<>asSiteCodes))
vTitle:=Get localized string("Select Sites")+"..."
Open window((Screen width/2)-146;(Screen height/2)-150;(Screen width/2)+146;(Screen height/2)+150;5;Get localized string("Select Sites"))
DIALOG([Facility];"SelectSites")
CLOSE WINDOW
For ($i;Size of array(asSiteCodes);1;-1)
If (asSigns{$i}#"â€¢")
DELETE FROM ARRAY(asSiteCodes;$i;1)
End if 
End for 
End if 
If (OK=1)
//Delete all existing records that are in selection
$Delete:=False
CREATE EMPTY SET([MerchPlan];"ToDelete")
For ($i;1;Size of array(asSiteCodes))
If (asSiteCodes{$i}#$FromSite)
QUERY([MerchPlan];[MerchPlan]MerchPlanID=asSiteCodes{$i}+"@")
For ($j;1;Records in selection([MerchPlan]))
ADD TO SET([MerchPlan];"ToDelete")
NEXT RECORD([MerchPlan])
End for 
End if 
End for 
USE SET("ToDelete")
DELETE SELECTION([MerchPlan])
//Pause to ensure at least 1 sec delay so that deletions occur b4 adds in replication
$Tickcount:=Tickcount
Repeat 
IDLE
Until (Tickcount>($Tickcount+90))
//Duplicate new codes to selection
For ($i;1;Size of array(asSiteCodes))
If (asSiteCodes{$i}#$FromSite)
For ($j;1;Size of array(asMerchPlan))
QUERY([MerchPlan];[MerchPlan]MerchPlanID=asMerchPlan{$j})
DUPLICATE RECORD([MerchPlan])
[MerchPlan]PrimaryKey:=0
[MerchPlan]MerchPlanID:=asSiteCodes{$i}+Substring([MerchPlan]MerchPlanID;6)
//[MerchPlan]DiscountAnalysis:=asSiteCodes{$i}+Substring([MerchPlan]DiscountAnalysis;6)
If (Length([MerchPlan]AnalysisCodes)>3)
$OldString:=Substring([MerchPlan]AnalysisCodes;1;5)
[MerchPlan]AnalysisCodes:=asSiteCodes{$i}+Substring([MerchPlan]AnalysisCodes;6)
[MerchPlan]AnalysisCodes:=Replace string([MerchPlan]AnalysisCodes;","+$OldString;","+asSiteCodes{$i})
End if 
SAVE RECORD([MerchPlan])
End for 
End if 
End for 
End if 
Else 
AlertMessage (Get localized string("There are no records to copy!"))
End if 
ALL RECORDS([Facility])
ORDER BY([Facility]SortOrder;[Facility]FacilityName)
REDRAW(MerchDiscPlanListBox)