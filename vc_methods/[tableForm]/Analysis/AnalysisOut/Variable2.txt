//%attributes = {"lang":"en"} comment added and reserved by 4D.
//Copy codes to other sites
C_LONGINT($i;$j;$x)
C_TEXT($FromSite)
CREATE SET([Analysis];"ToDo")
USE SET("UserSet")
If (Records in selection([Analysis])=0)
USE SET("ToDo")
End if 
ORDER BY([Analysis];[Analysis]SortOrder;>;[Analysis]Code;>)
If (Records in selection([Analysis])>0)
$FromSite:=Substring([Analysis]Code;1;5)
ARRAY TEXT($aAnalysis;0)
ARRAY LONGINT($aQuantity;0)
ARRAY LONGINT($aAlertQty;0)
ARRAY TEXT(asBillingPlan;0)
SELECTION TO ARRAY([Analysis]Code;asBillingPlan)
OK:=ConfirmThree (Get localized string("Duplicate")+" "+String(Records in selection([Analysis]))+" "+Get localized string("Analysis codes to")+" "+String(Size of array(<>asSiteCodes))+" "+Get localized string("other facilities?");Get localized string("Yes");Get localized string("Select Sites");Get localized string("No"))
If (OK=1) | (OK=2)
ARRAY TEXT(asFields;Size of array(<>asSiteCodes))
ARRAY TEXT(asSiteCodes;Size of array(<>asSiteCodes))
ARRAY TEXT(asSigns;Size of array(<>asSiteCodes))
For ($i;1;Size of array(<>asSiteCodes))
asFields{$i}:=<>asFacilities{$i}
asSiteCodes{$i}:=<>asSiteCodes{$i}
asSigns{$i}:=""
End for 
End if 
If (OK=2)
vTitle:="Select Sites..."
Open window((Screen width/2)-146;(Screen height/2)-150;(Screen width/2)+146;(Screen height/2)+150;5;Get localized string("Select Sites"))
DIALOG([Facility];"SelectSites")
CLOSE WINDOW
For ($i;Size of array(asSiteCodes);1;-1)
If (asSigns{$i}#"â€¢")
DELETE FROM ARRAY(asSiteCodes;$i;1)
End if 
End for 
End if 
If (OK=1)
//Delete all existing records that are in selection
If (aAnalysis=2)
ConfirmTwo (Get localized string("Delete all unused Analysis Receipt codes before duplication?");Get localized string("Yes");Get localized string("No"))
Else 
ConfirmTwo (Get localized string("Delete all unused Analysis Charge codes before duplication?");Get localized string("Yes");Get localized string("No"))
End if 
$Delete:=(OK=1)
CREATE EMPTY SET([Analysis];"ToDelete")
$Locked:=False
For ($i;1;Size of array(asSiteCodes))
If (asSiteCodes{$i}#$FromSite)
QUERY([Analysis];[Analysis]Code=asSiteCodes{$i}+"@";*)
If (aAnalysis=2)
QUERY([Analysis]; & ;[Analysis]SortOrder>50)
Else 
QUERY([Analysis]; & ;[Analysis]SortOrder<=50)
End if 
For ($j;1;Records in selection([Analysis]))
QUERY([Trxs];[Trxs]AnalysisCode=[Analysis]Code)
If ((Records in selection([Trxs])=0) & ($Delete)) | (Find in array(asBillingPlan;$FromSite+Substring([Analysis]Code;6))>0)
INSERT IN ARRAY($aAnalysis;0)
INSERT IN ARRAY($aQuantity;0)
INSERT IN ARRAY($aAlertQty;0)
$aAnalysis{1}:=[Analysis]Code
$aQuantity{1}:=[Analysis]InvQtyonHand
$aAlertQty{1}:=[Analysis]InvAlertQty
ADD TO SET([Analysis];"ToDelete")
If ($Locked=False)
$Locked:=Locked([Analysis])
If ($Locked)
AlertMessage (Get localized string("Analysis code:")+" "+[Analysis]Code+" "+Get localized string("is locked. Unable to copy analysis codes."))
End if 
End if 
End if 
NEXT RECORD([Analysis])
End for 
End if 
End for 
If ($Locked=False)
USE SET("ToDelete")
DELETE SELECTION([Analysis])
//Delete unused codes
//Pause to ensure at least 1 sec delay so that deletions occur b4 adds in replication
$Tickcount:=Tickcount
Repeat 
IDLE
Until (Tickcount>($Tickcount+90))
//Duplicate new codes to selection
For ($i;1;Size of array(asSiteCodes))
If (asSiteCodes{$i}#$FromSite)
For ($j;1;Size of array(asBillingPlan))
QUERY([Analysis];[Analysis]Code=asBillingPlan{$j})
DUPLICATE RECORD([Analysis])
[Analysis]PrimaryKey:=0
[Analysis]Code:=asSiteCodes{$i}+Substring([Analysis]Code;6)
If (aAnalysis=1)
$x:=Find in array($aAnalysis;[Analysis]Code)
If ($x>0)
[Analysis]InvQtyonHand:=$aQuantity{$x}
[Analysis]InvAlertQty:=$aAlertQty{$x}
Else 
[Analysis]InvQtyonHand:=0
End if 
End if 
viError:=0
ON ERR CALL("ErrorNone")
SAVE RECORD([Analysis])
ON ERR CALL("ErrorLog")
If (viError#0)
AlertMessage (Get localized string("An error number:")+" "+String(viError)+" "+Get localized string("occurred while trying to save record")+" "+[Analysis]Code+".  "+Get localized string("Please re-enter this record!"))
End if 
End for 
End if 
End for 
End if 
End if 
Else 
AlertMessage (Get localized string("There are no records to copy!"))
End if 