//%attributes = {"lang":"en"} comment added and reserved by 4D.
If (Form event=On Load)
C_LONGINT(vRCUpperActivate;vRCLowerActivate;vRCIntervalDays;vRCOccDays;vRentControlOn;vRCRoundRent)
C_REAL(vRCFixedIncrU;vRCPercentIncrU;vRCFixedIncrL;vRCPercentIncrL;vRCRoundCents)
C_TEXT(vRCEmailAddress)
vRCUpperActivate:=0
vRCLowerActivate:=0
vRCIntervalDays:=0
vRCOccDays:=0
vRentControlOn:=0
vRCRoundRent:=0
vRCFixedIncrU:=0
vRCPercentIncrU:=0
vRCFixedIncrL:=0
vRCPercentIncrL:=0
vRCEmailAddress:=""
LoadBackground 
Case of 
: (<>sProduct="@B")
OBJECT SET TITLE(*;"Text7";Get localized string("Maintenance - Types and Rates1B"))
OBJECT SET TITLE(*;"Text";Get localized string("Set which berth types you'd like to apply these adjustments to..."))
OBJECT SET TITLE(*;"Button";Get localized string("Select Berth Type"))
OBJECT SET TITLE(vRentControlOn;Get localized string("Yes, enable DRC for the selected berth types"))
: (<>sProduct="@R")
OBJECT SET TITLE(*;"Text7";Get localized string("Maintenance - Types and Rates1C"))
OBJECT SET TITLE(*;"Text";Get localized string("Set which appliance types you'd like to apply these adjustments to..."))
OBJECT SET TITLE(*;"Button";Get localized string("Select Appliance Type"))
OBJECT SET TITLE(vRentControlOn;Get localized string("Yes, enable DRC for the selected appliance types"))
: (<>sProduct="@C")
OBJECT SET TITLE(*;"Text7";Get localized string("Maintenance - Space Types and Rates"))
OBJECT SET TITLE(*;"Text";Get localized string("Set which space types you'd like to apply these adjustments to..."))
OBJECT SET TITLE(*;"Button";Get localized string("Select Space Type"))
OBJECT SET TITLE(vRentControlOn;Get localized string("Yes, enable DRC for the selected space types"))
Else 
OBJECT SET TITLE(*;"Text7";Get localized string("Maintenance - Types and Rates1A"))
OBJECT SET TITLE(*;"Text";Get localized string("Set which unit types you'd like to apply these adjustments to..."))
OBJECT SET TITLE(*;"Button";Get localized string("Select Unit Type"))
OBJECT SET TITLE(vRentControlOn;Get localized string("Yes, enable DRC for the selected unit types"))
End case 

ARRAY TEXT(asBillingPlan;0)
ARRAY TEXT(asBillingDesc;0)
QUERY([BillPlan];[BillPlan]Code=<>sFacility+"@";*)
QUERY([BillPlan]; & ;[BillPlan]Inactive=False)
ORDER BY([BillPlan];[BillPlan]SortOrder;>)
SELECTION TO ARRAY([BillPlan]Code;asBillingPlan)
SELECTION TO ARRAY([BillPlan]Description;asBillingDesc)
INSERT IN ARRAY(asBillingPlan;1)
INSERT IN ARRAY(asBillingDesc;1)
asBillingDesc{1}:="None"
asBillingDesc:=1

QUERY([UnitType];[UnitType]RUnitType=<>sFacility+"@")
//QUERY([UnitType]; & ;[UnitType]NoVacant>0)
ORDER BY([UnitType]SortOrder;>)
If (Records in selection([UnitType])>0)
SELECTION TO ARRAY([UnitType]Description;asFields;[UnitType]NoTotal;asTotalUnits;[UnitType]RUnitType;asUnitTypeR)
ARRAY TEXT(asSigns;Size of array(asFields))
For ($i;1;Size of array(asSigns))
asSigns{$i}:=""//"â€¢"
End for 
COPY ARRAY(asSigns;asUTSigns)
ARRAY LONGINT(asRentedUnits;Size of array(asUnitTypeR))
For ($i;1;Size of array(asUnitTypeR))
QUERY([Unit];[Unit]UnitTypeCode=asUnitTypeR{$i};*)
QUERY([Unit]; & ;[Unit]Vacant=False)
asRentedUnits{$i}:=Records in selection([Unit])
End for 
End if 

End if 