//%attributes = {"lang":"en"} comment added and reserved by 4D.

// ----------------------------------------------------
// User name (OS): unknown
// Date and time: 00/00/00, 00:00:00
// ----------------------------------------------------
// Method: [UserLogins].UserLoginsIn.Button2
// Description
// 
// Userlogins form, edit password button.
//
// Parameters
// ----------------------------------------------------

C_TEXT($Password)

$Password:=RequestMessage (Get localized string("Please enter new password for")+" "+[UserLogins]ActualName;"";Get localized string("OK");Get localized string("Cancel");"";Get localized string("Please enter the requested information");False;True)

If (Length($Password)>=[Facility]PasswordLength)
ARRAY LONGINT(aMatchPos;0)
ARRAY LONGINT(aMatchLength;0)
$Regex:=Match regex("(?=^.{1,255}$)((?=.*\\d)(?=.*[A-Z])(?=.*[a-z])|(?=.*\\d)(?=.*[^A-Za-z0-9])(?=.*[a-z])|(?=.*[^A-Za-z0-9])(?=.*[A-Z])(?=.*[a-z])|(?=.*\\d)(?=.*[A-Z])(?=.*[^A-Za-z0-9]))^.*";$Password;1;aMatchPos;aMatchLength)

If ([Facility]ComplexPassword=False) | ($Regex=True)
If ([Facility]MaxDaysPassword>0)
ConfirmTwo (Get localized string("Do you want the user to reset their password on next login?");Get localized string("Yes");Get localized string("No"))
If (OK=1)
[UserLogins]PasswordChangeDate:=!00-00-00!
Else 
[UserLogins]PasswordChangeDate:=Current date
End if 
End if 
OK:=1

Else 
AlertMessage (Get localized string("Your password must contain 3 of the following 4 - uppercase alpha characters, lowercase alpha characters, numeric digits or symbols"))
vLeg8:=""
vLeg9:=""
OK:=0
End if 
Else 
AlertMessage (Get localized string("Your password must be at least")+" "+String([Facility]PasswordLength)+" "+Get localized string("characters"))
vLeg8:=""
vLeg9:=""
OK:=0
End if 

If (OK=1)
[UserLogins]Password:=SecureEncryptHash ($Password)
End if 


