//%attributes = {"lang":"en"} comment added and reserved by 4D.
[UserLogins]AccessFacility:=""
If (<>sProduct="M@")
$All:=True
For ($i;1;Size of array(asFields))
If (asSigns{$i}="•")
[UserLogins]AccessFacility:=[UserLogins]AccessFacility+<>asSiteCodes{$i}+","
End if 
If ($All)
$All:=(asSigns{$i}="•")
End if 
End for 
If ($All)
[UserLogins]AccessFacility:="ALL"
End if 
End if 
If ([UserLogins]UserID="")
CreateUserID 
End if 
If ([UserLogins]ActualName="")
[UserLogins]ActualName:=[UserLogins]Name
End if 
[UserLogins]Permissions:=""
For ($i;1;Size of array(asPermissions))
If (asGranted{$i}="•")
[UserLogins]Permissions:=[UserLogins]Permissions+"Y"
Else 
[UserLogins]Permissions:=[UserLogins]Permissions+"N"
End if 
End for 
SAVE RECORD([UserLogins])
If (([Facility]GateSysType=2) | ([Facility]GateSysType=3) | ([Facility]GateSysType=4) | ([Facility]GateSysType=5) | ([Facility]GateSysType=7) | ([Facility]GateSysType=9)) & ([Facility]StaffPinsHOF=True)
If ((<>sProduct#"M@") & ([Facility]CentralAddress=""))
READ WRITE([Facility])
LOAD RECORD([Facility])
CREATE RECORD([StaffPINS])
[StaffPINS]Name:=[UserLogins]Name
[StaffPINS]PINNumber:=[UserLogins]PinCode
[StaffPINS]TimeZoneNo:=[UserLogins]Timezone
[StaffPINS]UserNo:=[UserLogins]UserNo
[StaffPINS]KeypadZone:=[UserLogins]KeypadZone
[StaffPINS]FacilityCode:=<>sFacility
SAVE RECORD([StaffPINS])
SAVE RECORD([Facility])
READ ONLY([Facility])
LOAD RECORD([Facility])
GateQueue ("CreateUser";[StaffPINS]UserNo;[StaffPINS]PINNumber;0;Substring([StaffPINS]Name;1;16);Current date;[StaffPINS]TimeZoneNo;0;0;0;0;String([StaffPINS]UserNo);False;[StaffPINS]KeypadZone)
End if 
End if 
If (Current user="Designer")
ALL RECORDS([UserLogins])
Else 
QUERY([UserLogins];[UserLogins]Name#"Daily";*)
QUERY([UserLogins]; & ;[UserLogins]Name#"Adminsupport";*)
QUERY([UserLogins]; & ;[UserLogins]Name#"Sync";*)
//18/01/17 DM Added extra queries for the HUB and for user Manager
If (<>sHOFacility="@HUB")
QUERY([UserLogins]; & ;[UserLogins]UserID=<>sFacility+"@";*)
End if 
QUERY([UserLogins]; & ;[UserLogins]Name#"Manager")
End if 
ORDER BY([UserLogins];[UserLogins]Name;>)
If (<>sHOFacility="@HUB")
QUERY([Groups];[Groups]GroupID=<>sFacility+"@")
Else 
ALL RECORDS([Groups])
End if 
ORDER BY([Groups];[Groups]Name;>)