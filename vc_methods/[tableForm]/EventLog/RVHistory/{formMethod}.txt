//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_TEXT(vUnitSize)
C_REAL(vListPrice;vVariance)
C_TIME(vtEventTime)
C_DATE(vdEventDate)
If (Form event=On Header)
vTitle:=Get localized string("Rental Variation History Report for ")+String(vSDate)+" "+Get localized string("to")+" "+String(vEDate)
vLegend:=Get localized string("List Prices are based on a Monthly Rate")
Case of 
: (<>sProduct="@B")
OBJECT SET TITLE(*;"Text1";Get localized string("Berth No"))
OBJECT SET TITLE(*;"Text3";Get localized string("Berth Size"))
: (<>sProduct="@R")
OBJECT SET TITLE(*;"Text1";Get localized string("Appl No"))
OBJECT SET TITLE(*;"Text3";Get localized string("Appliance Size"))
: (<>sProduct="@C")
OBJECT SET TITLE(*;"Text1";Get localized string("Space No"))
OBJECT SET TITLE(*;"Text3";Get localized string("Space Size"))
Else 
OBJECT SET TITLE(*;"Text1";Get localized string("Unit No"))
OBJECT SET TITLE(*;"Text3";Get localized string("Unit Size"))
End case 

If (BLOB size([Facility]ReportLogo_)#0)
LoadReferences (4)
End if 
End if 
If (Form event=On Printing Detail)
vtEventTime:=UTCTime ([EventLog]EventTime;False)
vdEventDate:=UTCDate ([EventLog]EventTime;False;[EventLog]EventDate)
vUnitNo:=Substring([EventLog]AgreeNo;6)
QUERY([Unit];[Unit]RUnitNo=[EventLog]AgreeNo)
If (Records in selection([Unit])=1)
vUnitSize:=[Unit]UnitSize
Else 
vUnitSize:=""
End if 
vListPrice:=Num([EventLog]BillPAnlys)
vVariance:=vListPrice-[EventLog]TrxAmount
End if 
If (Form event=On Printing Footer)
SetFooter 
End if 