//%attributes = {"lang":"en"} comment added and reserved by 4D.

READ WRITE(*)
DEFAULT TABLE([Agreement])
FORM SET INPUT([Agreement];"PettyCash")

//Create PETTY customer if doesn't exist
QUERY([Customer];[Customer]CustomerCode=<>sFacility+"PETTY")

If (Records in selection([Customer])=0)
CREATE RECORD([Customer])
[Customer]CustomerCode:=<>sFacility+"PETTY"
[Customer]CustomerName:="Petty Cash"
[Customer]NextMatch:=1
SAVE RECORD([Customer])
End if 


QUERY([Agreement];[Agreement]CustomerCode=<>sFacility+"PETTY")

If (Records in selection([Agreement])=0)
CREATE RECORD([Agreement])
[Agreement]AgreeNo:="PETTY"
[Agreement]RAgreeNo:=<>sFacility+"PETTY"
[Agreement]CustomerCode:=[Customer]CustomerCode
[Agreement]ActiveAgreemt:=False
[Agreement]MoveIn:=Current date
SAVE RECORD([Agreement])
End if 

Open window((Screen width/2)-421;(Screen height/2)-314;(Screen width/2)+421;(Screen height/2)+314;4;"Petty Cash Processing")


RELATE MANY([Agreement]RAgreeNo)

If (Records in selection([Agreement])=1)
MODIFY RECORD([Agreement];*)
Else 
AlertMessage (Get localized string("Multiple Agreements with Customer Code")+" "+<>sFacility+Get localized string("PETTY! Contact Storman Support to fix this issue."))
End if 

UNLOAD RECORD([Customer])
UNLOAD RECORD([Agreement])

READ ONLY(*)
CloseWindow 
UnloadAll 
SetFacility 