//%attributes = {"publishedSoap":true,"publishedWsdl":true,"lang":"en"} comment added and reserved by 4D.

// ----------------------------------------------------
// User name (OS): robert.richardson
// Date and time: 07/03/11, 06:21:08
// ----------------------------------------------------
// Method: WS_SetPassword
// Description
// Sets the password for a given customer code
//
// Parameters
// ----------------------------------------------------
//pwnt

SOAP DECLARATION(WS_Inputname;Is string var;SOAP input)
SOAP DECLARATION(WS_Password;Is string var;SOAP input)
SOAP DECLARATION(WS_WebServicePassword;Is text;SOAP input)

//WS_OLD=SOAP DECLARATION(WS_Edit;Is Boolean;SOAP Output)
SOAP DECLARATION(WS_ORSuccess;Is Boolean;SOAP output)
SOAP DECLARATION(WS_ORErrorCode;Is integer;SOAP output)
SOAP DECLARATION(WS_ORErrorDescription;Is string var;SOAP output)

$String:="Start WS_SetPassword"+Char(13)
$String:=$String+"WS_Inputname: "+WS_Inputname+Char(13)
$String:=$String+"WS_Password: "+WS_Password+Char(13)
C_TEXT($pass)
$pass:=WS_WebServicePassword
$pass:=Replace string($pass;Substring($pass;1;Length($pass)-2);"X"*(Length($pass)-2))
$String:=$String+"WS_WebServicePassword: "+$pass+Char(13)
WSRecordLog ($String)


QUERY([Facility];[Facility]FacilityCode=Substring(WS_Inputname;1;5))

If (WS_WebServicePassword=[Facility]ODBCPassword) & ([Facility]ODBCPassword#"")

If (WS_Password#"")

QUERY([Customer];[Customer]CustomerCode=WS_Inputname)
READ WRITE([Customer])

If (Records in selection([Customer])=1) & (Locked([Customer])=False)
[Customer]Password:=WS_Password
SAVE RECORD([Customer])

//WS_OLD=WS_Edit:=True
WS_ORSuccess:=True
WS_ORErrorCode:=0
WS_ORErrorDescription:="Customer password successfully changed."
Else 
//WS_OLD=WS_Edit:=False
WS_ORSuccess:=False
WS_ORErrorCode:=1012
WS_ORErrorDescription:="Customer record not found in database Storman."
End if 
Else 
WS_ORSuccess:=False
WS_ORErrorCode:=1029
WS_ORErrorDescription:="Invalid Customer password."
End if 

Else 
//WS_OLD=WS_Edit:=False
WS_ORSuccess:=False
WS_ORErrorCode:=1099
WS_ORErrorDescription:="Web Service Password is incorrect."
End if 

READ ONLY([Customer])
UNLOAD RECORD([Customer])

$String:="End WS_SetPassword"+Char(13)
$String:=$String+"WS_ORSuccess: "+String(WS_ORSuccess)+Char(13)
$String:=$String+"WS_ORErrorCode: "+String(WS_ORErrorCode)+Char(13)
$String:=$String+"WS_ORErrorDescription: "+WS_ORErrorDescription+Char(13)
WSRecordLog ($String)


ReadOnlyAll 
UnloadAll 
