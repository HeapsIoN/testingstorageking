//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_LONGINT($i)
C_DATE($Date)
C_TEXT($Analysis)
$Analysis:=RequestMessage (Get localized string("Analysis Code to search for:");"@DIS")
If (OK=1)
$Description:=RequestMessage (Get localized string("Description to search for:");"@")
If (OK=1)
READ WRITE([Trxs])
QUERY([Trxs];[Trxs]AnalysisCode=$Analysis;*)
QUERY([Trxs]; & ;[Trxs]TrxDate>=!2004-10-01!;*)
QUERY([Trxs]; & ;[Trxs]Description>=$Description;*)
QUERY([Trxs]; & ;[Trxs]TotAmt<-10)
ARRAY TEXT($aAgreeNo;0)
ARRAY DATE($aTrxDate;0)
ARRAY LONGINT($aRecNo;0)
SELECTION TO ARRAY([Trxs]AgreeNo;$aAgreeNo;[Trxs]TrxDate;$aTrxDate;[Trxs];$aRecNo)
Open window(50;50;400;250;Get localized string("Progress"))
For ($i;1;Size of array($aAgreeNo))
GOTO XY(2;2)
MESSAGE(Get localized string("Processing")+" "+String($i)+" "+Get localized string("of")+" "+String(Size of array($aAgreeNo))+"    ")
QUERY([Trxs];[Trxs]AgreeNo=$aAgreeNo{$i};*)
QUERY([Trxs]; & ;[Trxs]TrxDate=$aTrxDate{$i};*)
QUERY([Trxs]; & ;[Trxs]Charge=True;*)
QUERY([Trxs]; & ;[Trxs]DateTo>$aTrxDate{$i})
If (Records in selection([Trxs])>0)
If (Records in selection([Trxs])>1)
ORDER BY([Trxs];[Trxs]TotAmt;<)
End if 
$Date:=[Trxs]DateTo
GOTO RECORD([Trxs];$aRecNo{$i})
[Trxs]DateTo:=$Date
SAVE RECORD([Trxs])
End if 
End for 
CLOSE WINDOW
ARRAY TEXT($aAgreeNo;0)
ARRAY DATE($aTrxDate;0)
ARRAY LONGINT($aRecNo;0)
READ ONLY([Trxs])
UNLOAD RECORD([Trxs])
End if 
End if 