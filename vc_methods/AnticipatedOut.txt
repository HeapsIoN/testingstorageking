//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_BOOLEAN(forCompleted)
forCompleted:=False
LogAction (False;"Anticipated Move Outs Report started.")// records the report being started in the eventlog
If (PermissionsControl (76))
C_LONGINT($i;$j;$x;$y;$Choice)
OK:=1
If (<>tBatch=False)
ConfirmTwo (Get localized string("Print for weeks or months?");Get localized string("Months");Get localized string("Weeks"))
End if 
SetFacility 
$Choice:=OK
ARRAY DATE(adMonths;13)
If ($Choice=1)
adMonths{1}:=GetDate (1;Month of(Current date+30);Year of(Current date+30))-1
vLegend:=Get localized string("Anticipated Move-Outs by Month")
Else 
adMonths{1}:=Current date+(7-Day number(Current date))
vLegend:=Get localized string("Anticipated Move-Outs by Week")
End if 
For ($i;1;12)
If ($Choice=1)
adMonths{$i+1}:=Add to date(adMonths{$i}+1;0;1;0)-1
Else 
adMonths{$i+1}:=adMonths{$i}+7
End if 
End for 
ARRAY TEXT(aCustCode;0)
QUERY([UnitType];[UnitType]RUnitType=<>sFacility+"@")
ORDER BY([UnitType];[UnitType]RentedValue;>;[UnitType]RUnitType;>)
SELECTION TO ARRAY([UnitType]RUnitType;aCustCode)
ARRAY LONGINT(aiData;13;Size of array(aCustCode))
For ($i;1;13)
For ($j;1;Size of array(aCustCode))
aiData{$i}{$j}:=0
End for 
End for 
QUERY([Agreement];[Agreement]RAgreeNo=<>sFacility+"@";*)
QUERY([Agreement]; & ;[Agreement]CustomerCode#<>sFacility+"PETTY";*)
QUERY([Agreement]; & ;[Agreement]ActiveAgreemt=True)
For ($i;1;Records in selection([Agreement]))
If ([Agreement]AnticipateMoveOut>!2000-01-01!)
RELATE MANY([Agreement]RAgreeNo)
$x:=0
Repeat 
$x:=$x+1
Until ($x=13) | ([Agreement]AnticipateMoveOut<=adMonths{$x})
For ($j;1;Records in selection([Unit]))
$y:=Find in array(aCustCode;[Unit]UnitTypeCode)
If ($y>=1)
aiData{$x}{$y}:=aiData{$x}{$y}+1
End if 
NEXT RECORD([Unit])
End for 
End if 
NEXT RECORD([Agreement])
End for 
QUERY([UnitType];[UnitType]RUnitType=<>sFacility+"@")
ORDER BY([UnitType];[UnitType]RentedValue;>;[UnitType]RUnitType;>)
vTitle:=Get localized string("Schedule of Anticipated Move-Outs")
FORM SET OUTPUT([UnitType];"AnticipatedOut")
BREAK LEVEL(0)
ACCUMULATE(vTot1;vTot2;vTot3;vTot4;vTot5;vTot6;vTot7;vTot8;vTot9;vTot10;vTot11;vTot12;vTot13)
PrintSelection ("L";->[UnitType])
FORM SET OUTPUT([UnitType];"Output")
End if 
LogAction (True;"Anticipated Move Outs Report ended.")// records the report being started in the eventlog