//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_BOOLEAN(forCompleted)
forCompleted:=False
LogAction (False;"Notice to Vacate Report started.")// records the report being started in the eventlog
If (PermissionsControl (67))
DEFAULT TABLE([Unit])
OK:=1
If (<>tBatch=False)
Open window((Screen width/2)-200;(Screen height/2)-155;(Screen width/2)+200;(Screen height/2)+155;5;Get localized string("Notice to Vacate Report"))
DIALOG([Unit];"TermDialog")
CLOSE WINDOW
Else 
sAll:=1
sCust:=0
bAdd:=1
bMod:=0
End if 
If (OK=1)
SetFacility 
vLegend:=""
SET AUTOMATIC RELATIONS(True;False)
QUERY([Unit];[Unit]RUnitNo=<>sFacility+"@";*)
QUERY([Unit]; & ;[Agreement]ActiveAgreemt=True;*)
QUERY([Unit]; & ;[Agreement]OutNoticeDate#!00-00-00!)
If (bAdd=1)
ORDER BY([Unit];[Unit]UnitTypeCode;>;[Agreement]MoveOut;>;[Agreement]OutNoticeDate;>;[Agreement]ExpectedMoveOutTime;>)
BREAK LEVEL(1)
Else 
ORDER BY([Unit];[Agreement]MoveOut;>;[Agreement]OutNoticeDate;>;[Agreement]ExpectedMoveOutTime;>)
BREAK LEVEL(0)
End if 
SET AUTOMATIC RELATIONS(False;False)
ACCUMULATE([Unit]UnitAreaSize)
FORM SET OUTPUT([Unit];"Terminations")
PrintSelection ("P")
FORM SET OUTPUT([Unit];"Output")
End if 
End if 
LogAction (True;"Notice to Vacate Report ended.")