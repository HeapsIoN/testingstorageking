//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_LONGINT($i;$j)
C_DATE($NextValidDate)
DEFAULT TABLE([Agreement])
READ WRITE([Agreement])
READ WRITE([OtherChgs])
READ WRITE([Unit])
QUERY([Agreement];[Agreement]RAgreeNo=<>sFacility+"@";*)
QUERY([Agreement]; & ;[Agreement]ActiveAgreemt=True;*)
QUERY([Agreement]; & ;[Agreement]DisableAutoIncr=False;*)
QUERY([Agreement]; & ;[Agreement]MoveOut=!00-00-00!)
$WinRef:=Open window((Screen width/2)-410;(Screen height/2)-326;(Screen width/2)+410;(Screen height/2)+326;5;"Rental Increase Wizard")
DIALOG([Facility];"RentCalcNew")
CLOSE WINDOW($WinRef)
If (OK=1)
//Open window(20;50;300;150;0;Get localized string("Progress"))
$ProgressWin:=Open window(20;50;450;150;0)
//GOTO XY(2;2)
//MESSAGE(Get localized string("Calculating New Rents")+"..."+"    ")
FIRST RECORD([Agreement])

For ($i;1;Records in selection([Agreement]))
//GOTO XY(2;3)
//MESSAGE(Get localized string("Processing Agreement")+" : "+String($i)+" "+Get localized string("of")+" "+String(Records in selection)+"    ")
GOTO XY(2;2)
MESSAGE("Calculating new Rental Rates for Agreement "+String($i)+" of "+String(Records in selection([Agreement])))

$Total:=0
RELATE MANY([Agreement]RAgreeNo)
RELATE ONE([Agreement]BillingPlanCode)
If (bSelectNone=0)
FIRST RECORD([Unit])
For ($j;1;Records in selection([Unit]))
RELATE ONE([Unit]UnitTypeCode)
Case of 
: ([BillPlan]CasualBilling)
[Unit]DailyNewRent:=Round(CalculateBaseRate (2;0);2)
[Unit]WeeklyNewRentAmt:=Round([Unit]DailyNewRent*7;2)
[Unit]NewRentAmt:=Round([Unit]DailyNewRent*7/0.2308;2)
: ([BillPlan]PlanPeriod="Week")//Added in 3.3.3
[Unit]WeeklyNewRentAmt:=Round(CalculateBaseRate (2;0);2)
[Unit]NewRentAmt:=Round([Unit]WeeklyNewRentAmt/0.2308;2)
[Unit]DailyNewRent:=Round([Unit]WeeklyNewRentAmt/7;2)
Else 
[Unit]NewRentAmt:=Round(CalculateBaseRate (2;0);2)
[Unit]WeeklyNewRentAmt:=Round([Unit]NewRentAmt*0.2308;2)
[Unit]DailyNewRent:=Round([Unit]NewRentAmt*0.2308/7;2)
End case 
SAVE RECORD([Unit])
NEXT RECORD([Unit])
End for 
FIRST RECORD([Unit])

// $Total:=Round($Total*[Agreement]MthsAdv;2)
If (bSelect=1)
//[Agreement]NewRegularRent:=RoundAmt ($Total)  `Use Rates from File
Else 
//[Agreement]NewRegularRent:=RoundAmt ([Agreement]RegularRent*vNet/100+[Agreement]RegularRent)  `Apply percentage increase
APPLY TO SELECTION([Unit];[Unit]DailyNewRent:=RoundAmt (([Unit]DailyRentedValue*vNet/100)+[Unit]DailyRentedValue;vRentRoundMethod;vRentRound))
APPLY TO SELECTION([Unit];[Unit]WeeklyNewRentAmt:=RoundAmt (([Unit]WeeklyRentedValue*vNet/100)+[Unit]WeeklyRentedValue;vRentRoundMethod;vRentRound))
APPLY TO SELECTION([Unit];[Unit]NewRentAmt:=RoundAmt (([Unit]RentedValue*vNet/100)+[Unit]RentedValue;vRentRoundMethod;vRentRound))
End if 
FIRST RECORD([Unit])
Case of 
: ([BillPlan]CasualBilling=True)
[Agreement]NewRegularRent:=Sum([Unit]DailyNewRent)
: ([BillPlan]PlanPeriod="Week")
[Agreement]NewRegularRent:=Sum([Unit]WeeklyNewRentAmt)
Else 
[Agreement]NewRegularRent:=Sum([Unit]NewRentAmt)
End case 
If ([Agreement]NewRegularRent#[Agreement]RegularRent)
[Agreement]NewRentFrom:=[Agreement]NextBillDate
While ([Agreement]NewRentFrom<vMthDate)
[Agreement]NewRentFrom:=CalcNxtBillDate ([Agreement]NewRentFrom;[Agreement]MthsAdv;[Agreement]DaytoBill;[BillPlan]PlanPeriod)
End while 
If ([BillPlan]CasualBilling)
If ([BillPlan]PlanPeriod="Week")
$CasualAmount:=Round([Agreement]RegularRent*7;2)
$NewCasualAmount:=Round([Agreement]NewRegularRent*7;2)
Else 
$CasualAmount:=Round([Agreement]RegularRent*7/0.2308;2)
$NewCasualAmount:=Round([Agreement]NewRegularRent*7/0.2308;2)
End if 
Else 
$CasualAmount:=[Agreement]RegularRent
$NewCasualAmount:=[Agreement]NewRegularRent
End if 

If ([BillPlan]PeriodicBilling=True)
[Agreement]NewRentAmt:=RoundAmt ((RoundAmt ($NewCasualAmount*(100-[BillPlan]DiscPercentage)/100))*[Agreement]MthsAdv-[BillPlan]DiscFixedAmt)
Else 
[Agreement]NewRentAmt:=RoundAmt (($NewCasualAmount*[Agreement]MthsAdv*(100-[BillPlan]DiscPercentage)/100)-[BillPlan]DiscFixedAmt)
End if 

If ([BillPlan]PeriodicBilling=True)
$PosDate:=Add to date([Agreement]NextBillDate;0;([Agreement]PeriodicBillingPosition*-1);0)
If ([BillPlan]DiscOnlyMoveIn=True) & (($PosDate-[Agreement]MoveIn)>28)
[Agreement]NewRentAmt:=RoundAmt ($NewCasualAmount*[Agreement]MthsAdv)
End if 
Else 
If ([BillPlan]DiscOnlyMoveIn=True) & (([Agreement]NextBillDate-[Agreement]MoveIn)>28)
[Agreement]NewRentAmt:=RoundAmt ($NewCasualAmount*[Agreement]MthsAdv)
End if 
End if 

If ([BillPlan]PeriodicBilling=True)
[Agreement]NewDiscAmt:=Round((RoundAmt ($NewCasualAmount)*[Agreement]MthsAdv)-[Agreement]NewRentAmt;2)
Else 
[Agreement]NewDiscAmt:=Round(($NewCasualAmount*[Agreement]MthsAdv)-[Agreement]NewRentAmt;2)
End if 
Else 
[Agreement]NewRegularRent:=0
[Agreement]NewDiscAmt:=0
[Agreement]NewRentFrom:=!00-00-00!
[Agreement]NewRentAmt:=0
End if 
APPLY TO SELECTION([Unit];[Unit]IncreaseFrom:=[Agreement]NewRentFrom)
End if 


//Other Regular Charges increase here

If (pORCNone=0)
For ($j;1;Size of array(asORCAnalysis))
If (asORCSelect{$j}#"")
QUERY([Analysis];[Analysis]Code=asORCAnalysis{$j})
QUERY([OtherChgs];[OtherChgs]AgreeNo=[Agreement]RAgreeNo;*)
QUERY([OtherChgs]; & ;[OtherChgs]AnalysisCode=asORCAnalysis{$j})
If (Records in selection([OtherChgs])>0)
If (pORC=1)
APPLY TO SELECTION([OtherChgs];[OtherChgs]NewAmount:=[Analysis]DfltSellAmt)
Else 
APPLY TO SELECTION([OtherChgs];[OtherChgs]NewAmount:=RoundAmt (([OtherChgs]Amount*(vORCNet/100))+[OtherChgs]Amount;vOtherRoundMethod;vOtherRound))
End if 
$NextValidDate:=[Agreement]NextBillDate
While ($NextValidDate<vMthDate)
$NextValidDate:=CalcNxtBillDate ($NextValidDate;[Agreement]MthsAdv;[Agreement]DaytoBill;[BillPlan]PlanPeriod)
End while 
APPLY TO SELECTION([OtherChgs];[OtherChgs]IncreaseFrom:=$NextValidDate)
End if 
End if 
End for 
End if 


SAVE RECORD([Agreement])
NEXT RECORD([Agreement])
End for 

READ ONLY([Agreement])
READ ONLY([OtherChgs])
UNLOAD RECORD([OtherChgs])
UNLOAD RECORD([Agreement])

CLOSE WINDOW($ProgressWin)

RentIncrPendingRpt 
End if 