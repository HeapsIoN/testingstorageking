//%attributes = {"folder":"bpoint","lang":"en"} comment added and reserved by 4D.
// Method Name: wsc_bpoint_DeleteToken
// Syntax: wsc_bpoint_DeleteToken($username_t; $password_t; $merchantNumber_t; $token_t) -> text
// Created by: Robert.Richardson on: 15/05/12, 14:33:07
// Description:
//  Deletes a stored token

// Returns: 
C_TEXT($0;$errCode_t)
C_TEXT(errMessage_t)

// Required Parameters: 
C_TEXT($1;$username_t)
C_TEXT($2;$password_t)
C_TEXT($3;$merchantNumber_t)
C_TEXT($4;$token_t)

// Optional Parameters: --none--

// Other Variables:
C_TEXT($root)
C_TEXT($subelem)
C_TEXT($namespace)

errMessage_t:=""

$namespace:="urn:Eve"
$root:=DOM Create XML Ref("DeleteToken";$namespace)

$subelem:=DOM Create XML element($root;"/DeleteToken/username")
DOM SET XML ELEMENT VALUE($subelem;$1)

$subelem:=DOM Create XML element($root;"/DeleteToken/password")
DOM SET XML ELEMENT VALUE($subelem;$2)

$subelem:=DOM Create XML element($root;"/DeleteToken/merchantNumber")
DOM SET XML ELEMENT VALUE($subelem;$3)

$subelem:=DOM Create XML element($root;"/DeleteToken/token")
DOM SET XML ELEMENT VALUE($subelem;$4)

WEB SERVICE SET PARAMETER("XMLIn";$root)

WEB SERVICE CALL("https://www.bpoint.com.au/evolve/service.asmx";"urn:Eve/DeleteToken";"DeleteToken";"urn:Eve";Web Service manual)
$BpointError:=Num(WEB SERVICE Get info(0))

If ($BpointError=0)
C_BLOB($blob)
C_TEXT($resroot)
C_TEXT($ressubelem)
WEB SERVICE GET RESULT($blob;"XMLOut";*)
$resroot:=DOM Parse XML variable($blob)

$ressubelem:=DOM Find XML element($resroot;"/DeleteTokenResponse/response/ResponseCode")
If ($ressubelem#"0000000000000000")
DOM GET XML ELEMENT VALUE($ressubelem;$errCode_t)
End if 

$ressubelem:=DOM Find XML element($resroot;"/DeleteTokenResponse/response/ResponseMessage")
If ($ressubelem#"0000000000000000")
DOM GET XML ELEMENT VALUE($ressubelem;errMessage_t)
End if 
DOM CLOSE XML($resroot)

End if 

DOM CLOSE XML($root)

$0:=$errCode_t
