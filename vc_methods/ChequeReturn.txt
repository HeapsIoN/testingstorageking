//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_TEXT($Cust;$Agreemt)
C_DATE($Date)
C_REAL($Amt)
C_TEXT($TrxsDrawer;$TrxsBankBranch)//-- Added by Hamid - 7154
$Cust:=Substring([Trxs]CustomerCode;6)
$Agreemt:=Substring([Trxs]AgreeNo;6)
$Date:=Current date
Repeat 
If ([Reservation]RReserveNo>"")
$Date:=RequestDate (Get localized string("Customer")+": "+$Cust+" "+Get localized string("Reservation")+": "+$Agreemt+". "+Get localized string("Return Date")+"?";$Date)
Else 
$Date:=RequestDate (Get localized string("Customer")+": "+$Cust+" "+Get localized string("Agreement")+": "+$Agreemt+". "+Get localized string("Return Date")+"?";$Date)
End if 
If (OK=1) & (($Date<[Facility]CantModPrior) | ($Date<[Trxs]TrxDate))
Case of 
: ($Date<[Facility]CantModPrior)
AlertMessage (Get localized string("Date cannot be earlier than the Cant Modify Prior To date of")+" "+String([Facility]CantModPrior)+".")
$Date:=[Facility]CantModPrior
: ($Date<[Trxs]TrxDate)
AlertMessage (Get localized string("Date cannot be earlier than the original transaction date of")+" "+String([Trxs]TrxDate)+".")
$Date:=[Trxs]TrxDate
Else 
//Do Nothing
End case 
OK:=2
End if 
Until (OK<=1)
If (OK=1)
$Amt:=[Trxs]TotAmt
RELATE ONE([Trxs]CustomerCode)
[Customer]Balance:=[Customer]Balance+$Amt
//Create negative receipt
$TrxsDrawer:=[Trxs]Drawer//-- Added by Hamid - 7154
$TrxsBankBranch:=[Trxs]BankBranch//-- Added by Hamid - 7154
CREATE RECORD([Trxs])
If ([Reservation]RReserveNo>"")
[Trxs]CustomerCode:=[Reservation]CustomerCode
[Trxs]AgreeNo:=[Reservation]RReserveNo
Else 
[Trxs]CustomerCode:=[Agreement]CustomerCode
[Trxs]AgreeNo:=[Agreement]RAgreeNo
End if 
[Trxs]TrxDate:=$Date
[Trxs]Charge:=False
QUERY([Analysis];[Analysis]Code=<>sFacility+"@";*)
QUERY([Analysis]; & ;[Analysis]Inactive=False;*)
QUERY([Analysis]; & ;[Analysis]SpecialOptions=52)
If (Records in selection([Analysis])=0)
QUERY([Analysis];[Analysis]Code=<>sFacility+"@";*)
QUERY([Analysis]; & ;[Analysis]Inactive=False;*)
QUERY([Analysis]; & ;[Analysis]SortOrder>50;*)
QUERY([Analysis]; & ;[Analysis]SortOrder<=95)
ORDER BY([Analysis];[Analysis]SortOrder;>)
End if 
If ([Analysis]SetBankedDate=True)
[Trxs]DateBanked:=[Trxs]TrxDate
End if 
[Trxs]AnalysisCode:=[Analysis]Code
[Trxs]Description:=[Analysis]Description
[Trxs]TotAmt:=$Amt*-1
[Trxs]Confirmed:=True
[Trxs]Drawer:=$TrxsDrawer//-- Added by Hamid - 7154
[Trxs]BankBranch:=$TrxsBankBranch//-- Added by Hamid - 7154
SAVE RECORD([Trxs])
//Create Returned Cheque Charge
QUERY([Analysis];[Analysis]Code=<>sFacility+"@";*)
QUERY([Analysis]; & ;[Analysis]SpecialOptions=2;*)
QUERY([Analysis]; & ;[Analysis]Inactive=False)
If ([Analysis]DfltSellAmt>0)
CREATE RECORD([Trxs])
If ([Reservation]RReserveNo>"")
[Trxs]CustomerCode:=[Reservation]CustomerCode
[Trxs]AgreeNo:=[Reservation]RReserveNo
Else 
[Trxs]CustomerCode:=[Agreement]CustomerCode
[Trxs]AgreeNo:=[Agreement]RAgreeNo
End if 
[Trxs]TrxDate:=$Date
[Trxs]Charge:=True
[Trxs]AnalysisCode:=[Analysis]Code
[Trxs]Description:=[Analysis]Description
[Trxs]TotAmt:=[Analysis]DfltSellAmt
[Trxs]TaxAmt:=CalcTaxAmt ([Trxs]TotAmt)
[Trxs]Confirmed:=True
SAVE RECORD([Trxs])
[Customer]Balance:=[Customer]Balance+[Analysis]DfltSellAmt
If ([Trxs]TrxDate>[Customer]LatestTrxDate)
[Customer]LatestTrxDate:=[Trxs]TrxDate
End if 
End if 
SAVE RECORD([Customer])
End if 