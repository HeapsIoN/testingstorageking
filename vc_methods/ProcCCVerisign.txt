//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_TEXT($0)//Authorization Code
C_BOOLEAN($1)//Display Decline / Error Alerts
C_TEXT($0;$Type)//S, C or V
C_REAL($3)//Transaction Amount
C_TEXT($4)//Card Number
C_TEXT($5)//Expiry Date
C_TEXT($6)//OrigID
C_TEXT($7;$8;$9)//AgreeNo,Description,CustomerCode
C_TEXT($10;$11)//Customer Name, Address
C_TEXT($12)//City
C_TEXT($13)//Zip Code
C_TEXT($14;$15;$16)//Account,Login,Password
//C_LONGINT($17;$18)  `Logging,Timeout
C_TEXT($Message;$Header;$Response;$Reply;$TransactionResponse;$HTTPResponse;$TransResult)
C_LONGINT($TCPID;$Error)
$cr:=Char(13)+Char(10)
$TCPID:=0
$Buffer:=""
$Status:=0
$ServerPort:="443"
$ServerAddress:="payments.verisign.com.au"
$Folder:="/payflowpost"//also had -test

$Response:=""
$Header:=""
$Message:=""
$Reply:=""
$TransactionResponse:=""
$HTTPResponse:=""
$TransResult:=""
$0:=""

$Type:=$2
If ($Type="P")
$Type:="S"
End if 

//Build the transaction message
$Message:=$Message+"TRXTYPE="+$Type
$Message:=$Message+"&TENDER="+"C"
$Message:=$Message+"&PARTNER="+"VSA"
$Message:=$Message+"&VENDOR="+$14
$Message:=$Message+"&USER="+$15
$Message:=$Message+"&PWD="+$16

Case of 
: ($Type="S")//Sale
$Message:=$Message+"&AMT="+String(Round($3;2);"#######.00")
$Message:=$Message+"&ACCT="+$4
$Message:=$Message+"&EXPDATE="+Substring($5;1;2)+Substring($5;3;2)
: ($Type="C")//Credit
$Message:=$Message+"&ORIGID="+$6
If ($3>0)
$Message:=$Message+"&AMT="+String(Round($3;2);"#######.00")
End if 
: ($Type="V")//Void
$Message:=$Message+"&ORIGID="+$6
Else 
//do nothing
End case 
$Message:=$Message+"&COMMENT1="+$7
$Message:=$Message+"&COMMENT2="+$9

$Header:=$Header+"POST "+$Folder+" HTTP/1.1"+$cr
$Header:=$Header+"Host: "+$ServerAddress+":"+$ServerPort+$cr
$Header:=$Header+"Accept: "+"*/*"+$cr
$Header:=$Header+"Pragma: no-cache"+$cr
$Header:=$Header+"Connection: Keep-Alive"+$cr//
$Header:=$Header+"Content-type: text/plain; charset=ISO-8859-4"+$cr
$Header:=$Header+"Content-length: "+String(Length($Message))+$cr

$Error:=TCP_Open ($ServerAddress;Num($ServerPort);$TCPID;3)

If ($Error=0)
MaskCCLog ($Message;"Verisign")
$Error:=TCP_Send ($TCPID;$Header+$cr+$Message+$cr+$cr)
$Reply:=""
$WaitTime:=Current time+?00:00:15?//Timeout after 15 seconds

Repeat //While a HTTP response is being received
$Error:=TCP_Receive ($TCPID;$Buffer)
$Error:=TCP_State ($TCPID;$Status)
$Reply:=$Reply+$Buffer
If (Current time>$WaitTime)
$Error:=1//Timeout
End if 
Until ($Status=0) | ($Error>0) | (($Buffer="") & (Length($Reply)>10))
$Error:=TCP_Close ($TCPID)


//There is either an error '$Error' or a reply from the Verisign Server
//An HTTP response may contain headers with Codes of 300, 340 etc, these mean that the HTTP communication is active but it maybe paused
//or similar telling us not to give up on the communication but not to fully process it yet.   We need to loop through the HTTP response until we get to 
//a header with a response code of 200, when we do we process it.
Case of 
: ($Error>1)
$0:="ERRTCP"
AlertMessage (Get localized string("TCP Sending Error")+" # "+String($Error))
: ($Error=1)
$0:="ERR4TO"
AlertMessage (Get localized string("TCP Timeout Error"))
Else 
Repeat //While a complete HTTP request and response is being received
$HTTPResponse:=Substring($Reply;1;Position($cr+$cr;$Reply)-1)
If (Position("HTTP";$HTTPResponse)>0)
$HTTPResponseCode:=Substring($HTTPResponse;Position(" ";$HTTPResponse)+1;3)//This will be e.g. 100 (Continue), 200 (OK) etc
$Reply:=Substring($Reply;Position($cr+$cr;$Reply)+Length($cr+$cr))
Else //If not an HTTPResponse Header then must be the Verisign Payment Response message content
Case of 
: (Num($HTTPResponseCode)>=200) & (Num($HTTPResponseCode)<300)
$TransactionResponse:=$Reply
Else 
$TransactionResponse:=" "//To Break out of the Until loop
$Error:=Num($HTTPResponseCode)
End case 
End if 
Until ($TransactionResponse#"")
End case 


//At this point we either end up with an Error '$Error', or a string value for the '$TransactionResponse'.
If ($Error=0)
$TransactionResponse:=Substring($TransactionResponse;Position("Result";$TransactionResponse))+$cr+$cr+$cr//So we know for sure where the end is
$TransactionResponse:=Substring($TransactionResponse;1;Position($cr+$cr+$cr;$TransactionResponse)-Length($cr))

//Parse the returned HTML response
C_TEXT($Parameter)
C_LONGINT($Pos)
ARRAY TEXT(atResultName;0)
ARRAY TEXT(atResultValue;0)
$TransactionResponse:=$TransactionResponse+"&"
Repeat 
$Parameter:=""
$Name:=""
$Value:=""
$Pos:=Position("&";$TransactionResponse)
$Parameter:=Substring($TransactionResponse;1;$Pos-1)
$Name:=Substring($Parameter;1;Position("=";$Parameter)-1)
$Value:=Substring($Parameter;Position("=";$Parameter)+1)
INSERT IN ARRAY(atResultName;1;1)
INSERT IN ARRAY(atResultValue;1;1)
atResultName{1}:=$Name
atResultValue{1}:=$Value
$TransactionResponse:=Substring($TransactionResponse;$Pos+1)
Until (Length($TransactionResponse)=0)
$TransactionResponse:=""


$Index:=Find in array(atResultName;"AUTHCODE")
If ($Index#-1)
$0:=Substring(atResultValue{Find in array(atResultName;"AUTHCODE")};1;6)
End if 


//If we can't find the 'Result' parameter then there is no point in continuing and an 'Error' is set
$Index:=Find in array(atResultName;"RESULT")
If ($Index#-1)
$TransResult:=atResultValue{$Index}//0=approved, >0=declined
Case of 
: ($TransResult="0")
$Index:=Find in array(atResultName;"PNREF")
If ($Index#-1)
[Trxs]CCTransID:=atResultValue{Find in array(atResultName;"PNREF")}//Return the TransID
End if 
Else 
$0:="DEC"
$Index:=Find in array(atResultName;"RESPMSG")
If ($1) & ($Index#-1)
AlertMessage (Get localized string("Result Code")+": "+$TransResult+" - "+atResultValue{Find in array(atResultName;"RESPMSG")})
End if 
End case 
End if 
End if 
Else 
AlertMessage (Get localized string("The TCP connection could not be opened. Error")+" #"+String($Error))
$0:="ERR4IC"
End if 