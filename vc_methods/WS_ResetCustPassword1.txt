//%attributes = {"publishedSoap":true,"publishedWsdl":true,"lang":"en"} comment added and reserved by 4D.
// ----------------------------------------------------
// User name (OS): jonathon.wildon
// Date and time: 02/12/15, 10:45:08
// API Version: 1.0
// ----------------------------------------------------
// Method: WS_ResetCustPassword
// Description
// Resets the password for a given customer code.
//
// Parameters
// ----------------------------------------------------

SOAP DECLARATION(WS_CustomerID;Is string var;SOAP input)
SOAP DECLARATION(WS_WebServicePassword;Is text;SOAP input)

SOAP DECLARATION(WS_ORSuccess;Is Boolean;SOAP output)
SOAP DECLARATION(WS_ORErrorCode;Is integer;SOAP output)
SOAP DECLARATION(WS_ORErrorDescription;Is string var;SOAP output)

$String:="Start WS_ResetCustPassword"+Char(13)
$String:=$String+"WS_CustomerID: "+WS_CustomerID+Char(13)
$String:=$String+"WS_WebServicePassword: "+WS_WebServicePassword+Char(13)
C_TEXT($pass)
C_BLOB($MessageBodyBlob)//Added By Hamid 08/12/2015
$pass:=WS_WebServicePassword
$pass:=Replace string($pass;Substring($pass;1;Length($pass)-2);"X"*(Length($pass)-2))
$String:=$String+"WS_WebServicePassword: "+$pass+Char(13)
WSRecordLog ($String)

TRACE
QUERY([Facility];[Facility]FacilityCode=Substring(WS_CustomerID;1;5))

If (WS_WebServicePassword=[Facility]ODBCPassword) & ([Facility]ODBCPassword#"")

QUERY([Customer];[Customer]CustomerCode=WS_CustomerID)
READ WRITE([Customer])

If (Records in selection([Customer])=1) & (Locked([Customer])=False)

If ([Customer]EmailAddress#"")

[Customer]Password:=Substring(Generate UUID;1;10)
SAVE RECORD([Customer])

QUERY([Agreement];[Agreement]CustomerCode=WS_CustomerID)
If (Records in selection([Agreement])=0)
QUERY([Reservation];[Reservation]CustomerCode=WS_CustomerID)
End if 

QUERY([Notices];[Notices]Description="Password Reset";*)
QUERY([Notices];[Notices]FacilityCode=[Facility]FacilityCode;*)
QUERY([Notices]; & ;[Notices]NoticePlanNo=[Agreement]NoticePlanNo)


If (Records in selection([Notices])=1)

CREATE RECORD([Letters])
[Letters]AgreeNo:=[Agreement]RAgreeNo
If (Length([Letters]AgreeNo)=0)
[Letters]AgreeNo:=[Reservation]RReserveNo
End if 
[Letters]LetterBLOB:=[Notices]Email
[Letters]LetterDesc:=[Notices]Description
$p:=->[Notices]Email
[Letters]SentViaEmail:=True
If ([Notices]EmailSubject#"")
[Letters]Custom1:=[Notices]EmailSubject
Else 
[Letters]Custom1:=[Letters]LetterDesc
End if 
[Letters]SentwithInv:=False
[Letters]DateSent:=Current date
[Letters]TimeSent:=Current time
BLOB PROPERTIES($p->;$Compressed)
If ($Compressed#Is not compressed)
EXPAND BLOB($p->)
End if 
$emailText:=Convert to text($p->;"UTF-8")
$emailText:=ProcessStormanHTMLTags ($emailText)
CONVERT FROM TEXT($emailText;"UTF-8";[Letters]LetterBLOB)
[Letters]Custom1:=ProcessStormanHTMLTags ([Letters]Custom1)
COMPRESS BLOB([Letters]LetterBLOB)

If ([Letters]AgreeNo#"") | ([Letters]ContactNo#"")
SAVE RECORD([Letters])
End if 

CREATE RECORD([Message])
[Message]Subject:=[Letters]Custom1
[Message]Address:=[Customer]EmailAddress
[Message]OutIn:=True
[Message]MsgDate:=Current date
[Message]MsgTime:=Current time
[Message]Facility:=WS_ORFacilityCode
BLOB PROPERTIES([Letters]LetterBLOB;$Compressed)
If ($Compressed#Is not compressed)
EXPAND BLOB([Letters]LetterBLOB)
End if 
[Message]EmailMsg:=[Letters]LetterBLOB
COMPRESS BLOB([Letters]LetterBLOB)
SAVE RECORD([Message])

SendMail 
SetFacility 
WS_ORSuccess:=True
WS_ORErrorCode:=0
WS_ORErrorDescription:=""

Else 

//$MessageBody:="Hi "+[Customer]CustomerName+","+Char(Carriage return)+Char(Carriage return)
//$MessageBody:=$MessageBody+"We have received a request to reset your password. Please find your new temporary password below:"+Char(Carriage return)

$MessageBody:="<html><head></head><body><p>Hi "+Reverse ([Customer]CustomerName;[Facility]AutoFirstName)+",<br><br>"//Added By Hamid 08/12/2015
$MessageBody:=$MessageBody+"<p>We have received a request to reset your password. Please find your new temporary password below:<p>"
$MessageBody:=$MessageBody+[Customer]Password+"<p>"
//**10743
$MessageBody:=$MessageBody+"If you have any questions or trouble logging in please contact us. If you did not request a new password, please let us know immediately by replying to this email. Thank you.<p>"
$MessageBody:=$MessageBody+[Facility]FacilityName+"<p>"
If ([Facility]Phone#"")
$MessageBody:=$MessageBody+"Phone: "+[Facility]Phone+",<p>"
End if 
If ([Facility]EmailAddress#"")
$MessageBody:=$MessageBody+"Email: "+[Facility]EmailAddress+",<p>"
End if 
//**
$MessageBody:=$MessageBody+"</body></html>"
$MessageBody:=ProcessStormanHTMLTags ($MessageBody)
$OK:=OR_NewMailMessage ([Facility]EmailAddress;[Customer]EmailAddress;"";"";"Password Reset";$MessageBody;False;->Attachments;"";"";"";"";"";"";"")//Notification Email to Facility

CREATE RECORD([Letters])
[Letters]AgreeNo:=[Agreement]RAgreeNo
If (Length([Letters]AgreeNo)=0)
[Letters]AgreeNo:=[Reservation]RReserveNo
End if 

//CONVERT FROM TEXT($MessageBody;"UTF-8";[Letters]LetterBLOB)
CONVERT FROM TEXT($MessageBody;"UTF-8";$MessageBodyBlob)//Added By Hamid 08/12/2015
//[Letters]LetterBLOB:=TEXT TO BLOB($MessageBody;[Letters]LetterBLOB;Mac text without length)

//[Letters]LetterBLOB:=$MessageBody
[Letters]LetterBLOB:=$MessageBodyBlob//Added By Hamid 08/12/2015
[Letters]LetterDesc:="Password Reset"
[Letters]SentViaEmail:=True
[Letters]Custom1:="Password Reset"
[Letters]Custom1:=[Letters]LetterDesc
[Letters]SentwithInv:=False
[Letters]DateSent:=Current date
[Letters]TimeSent:=Current time
If ([Letters]AgreeNo#"") | ([Letters]ContactNo#"")
SAVE RECORD([Letters])
End if 

WS_ORSuccess:=True
WS_ORErrorCode:=0
WS_ORErrorDescription:=""

End if 

Else 
WS_ORSuccess:=False
WS_ORErrorCode:=1054
WS_ORErrorDescription:="Customer email address not defined in database Storman. Password could not be reset."
End if 

Else 
WS_ORSuccess:=False
WS_ORErrorCode:=1012
WS_ORErrorDescription:="Customer record not found in database Storman."
End if 

Else 
WS_ORSuccess:=False
WS_ORErrorCode:=1099
WS_ORErrorDescription:="Web Service Password is incorrect."
End if 

READ ONLY([Customer])
UNLOAD RECORD([Customer])

$String:="End WS_ResetCustPassword"+Char(13)
$String:=$String+"WS_ORSuccess: "+String(WS_ORSuccess)+Char(13)
$String:=$String+"WS_ORErrorCode: "+String(WS_ORErrorCode)+Char(13)
$String:=$String+"WS_ORErrorDescription: "+WS_ORErrorDescription+Char(13)
WSRecordLog ($String)


ReadOnlyAll 
UnloadAll 
