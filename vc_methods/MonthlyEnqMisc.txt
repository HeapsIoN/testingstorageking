//%attributes = {"lang":"en"} comment added and reserved by 4D.

// ----------------------------------------------------
// User name (OS): nicholas.wykes
// Date and time: 10/12/10, 13:44:44
// ----------------------------------------------------
// Method: MonthlyEnqMisc
// Description
// Various construction methods used by MonthlyEnq
//
// Parameters
// ----------------------------------------------------

C_LONGINT($1;$DateDifference;$Period)
If (b6P=1)
$Period:=6
Else 
$Period:=12
End if 

Case of 
: ($1=1)//Increment Arrays
INSERT IN ARRAY(rpt2D;Size of array(rpt2D)+1)
INSERT IN ARRAY(rpt2D{Size of array(rpt2D)};0;$Period+1)
: ($1=2)//Update Start/End Dates

Case of 
: (asPeriodList=1)//Yearly
dStartDate:=Date("01/01/"+String(Year of(Current date)-($Period-1)))
dEndDate:=Add to date(dStartDate;$Period;0;-1)
: (asPeriodList=2)//Monthly
dStartDate:=Date("01/"+String(Month of(Current date))+"/"+String(Year of(Current date)))
dStartDate:=Add to date(dStartDate;0;($Period*-1)+1;0)
dEndDate:=Add to date(dStartDate;0;$Period;-1)
: (asPeriodList=3)//Weekly
$DateDifference:=Abs(Day number(Current date)-[Facility]WeekEndDay)
dStartDate:=Add to date(Current date;0;0;(($Period*7)+$DateDifference-1)*-1)
dEndDate:=Add to date(dStartDate;0;0;($Period*7)-1)
: (asPeriodList=4)//Daily
dStartDate:=Add to date(Current date;0;0;-1*($Period))
dEndDate:=Add to date(dStartDate;0;0;$Period-1)
: (asPeriodList=5)//Half-Daily
dStartDate:=Add to date(Current date;0;0;-1*($Period/2))
dEndDate:=Add to date(dStartDate;0;0;($Period/2)-1)
End case 
: ($1=3)//Update End Dates Only
Case of 
: (asPeriodList=1)//Yearly
dEndDate:=Add to date(dStartDate;$Period;0;-1)
: (asPeriodList=2)//Monthly
dEndDate:=Add to date(dStartDate;0;$Period;-1)
: (asPeriodList=3)//Weekly
dEndDate:=Add to date(dStartDate;0;0;($Period*7)-1)
: (asPeriodList=4)//Daily
dEndDate:=Add to date(dStartDate;0;0;$Period-1)
: (asPeriodList=5)//Half-Daily
dEndDate:=Add to date(dStartDate;0;0;($Period/2)-1)
End case 
End case 

