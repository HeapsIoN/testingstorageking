//%attributes = {"lang":"en"} comment added and reserved by 4D.
//With a database that is being upgraded to version of Storman that uses Primary Keys, the Transactions at the sites must be assigned a Primary Key value from the HOF Facility range.
//To achieve this we perform the following functions:
//- Delete the Take Payment Transactions from both HOF and site (after exporting)
//- Import the Take Payment Transactions (Exported from HOF) into HOF - this will assign them a HOF Primary Key value
//- Run this method which will create a Transaction at each of the Two sites involved in the Take Payment process

ARRAY TEXT($asFacilityCode;0)
C_TEXT($Desc)
C_BLOB(LogBlob)

CREATE EMPTY SET([Trxs];"TakePaymentTrxs")
CREATE EMPTY SET([Trxs];"Selected")
SELECTION TO ARRAY([Facility]FacilityCode;$asFacilityCode)

For ($FacilityCounter;1;Size of array($asFacilityCode))
$FacilityCode:=$asFacilityCode{$FacilityCounter}
QUERY([Trxs];[Trxs]CustomerCode=($FacilityCode+"@");*)
QUERY([Trxs]; & ;[Trxs]AnalysisCode#($FacilityCode+"@");*)
QUERY([Trxs]; & ;[Trxs]AnalysisCode#"";*)
QUERY([Trxs]; & ;[Trxs]Charge=False)
CREATE SET([Trxs];"Selected")
UNION("Selected";"TakePaymentTrxs";"TakePaymentTrxs")
End for 

USE SET("TakePaymentTrxs")
FIRST RECORD([Trxs])

$Array:=Get pointer("â—ŠasFlags"+Table name(6))
For ($TransactionCounter;1;Records in selection([Trxs]))
$Desc:=""
SET BLOB SIZE(LogBlob;0)
For ($i;1;CountFields (6))
$p:=Field(6;$i)
If (Field name($p)="UserLogin")
If ($p->="")
$p->:=[UserLogins]Name
End if 
End if 
If (Substring($Array->{$i};1;1)="L")
If (Substring($Array->{$i};2;1)="A")
$Alert:=True
End if 
//Case of 
//: (Type($p->)=Is Subtable)
//$LogSubtable:=""`GetSubtableValue (6;$p)`replace with blank string as not using any more but 
//End case 
$LogSubtable:=""//not needed anymore but passing as parameter for now
$Desc:=$Desc+String($i;"000")+Field name($p)+Char(19)+GetFieldValue ($p)+Char(20)
End if 
End for 
LogEvent ($Desc;$Alert;"ADD";6;"037";String([Trxs]PrimaryKey);[Trxs]AnalysisCode;[Trxs]TrxDate;[Trxs]TotAmt;$LogSubtable;->LogBlob;"";Substring([Trxs]AgreeNo;1;5);[Trxs]AgreeNo)//Site where Payment was taken
LogEvent ($Desc;$Alert;"ADD";6;"037";String([Trxs]PrimaryKey);[Trxs]AnalysisCode;[Trxs]TrxDate;[Trxs]TotAmt;$LogSubtable;->LogBlob;"";Substring([Trxs]AnalysisCode;1;5);[Trxs]AnalysisCode)//Site Payment was taken For
NEXT RECORD([Trxs])
End for 
CLEAR SET("TakePaymentTrxs")
CLEAR SET("Selected")

//ImportTakePaymentRecords