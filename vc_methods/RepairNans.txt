//%attributes = {"folder":"CustomSearch","lang":"en"} comment added and reserved by 4D.
C_LONGINT($i;$TableNo)
C_POINTER($Field)
$TableNo:=Table(Current default table)
ARRAY TEXT(asFields;1)
ARRAY LONGINT(aiCheck;1)
asFields{1}:="All Numbers"
For ($i;1;Get last field number(Current default table))
$Field:=Field($TableNo;$i)
If (Type($Field->)=Is real)
INSERT IN ARRAY(asFields;Size of array(asFields)+1)
asFields{Size of array(asFields)}:=Field name($Field)
INSERT IN ARRAY(aiCheck;Size of array(aiCheck)+1)
aiCheck{Size of array(aiCheck)}:=$i
End if 
End for 
If (Size of array(asFields)>1)
asFields:=1
Open window((Screen width/2)-159;(Screen height/2)-89;(Screen width/2)+159;(Screen height/2)+89;5;Get localized string("Select Field"))
DIALOG([Facility];"SelectField")
CLOSE WINDOW
If (OK=1)
READ WRITE(vpFile->)
If (asFields=1)
Open window(20;50;300;150;0;Get localized string("Progress"))
Repeat 
asFields:=asFields+1
$Field:=Field($TableNo;aiCheck{asFields})
GOTO XY(2;2)
MESSAGE(Get localized string("Searching")+" "+Field name($Field)+"...                   ")
RepairFixNans ($Field)
Until (asFields=Size of array(asFields))
CLOSE WINDOW
Else 
$Field:=Field($TableNo;aiCheck{asFields})
RepairFixNans ($Field)
End if 
READ ONLY(vpFile->)
UNLOAD RECORD(vpFile->)
End if 
Else 
AlertMessage (Get localized string("This table does not have any fields that could contains NANS!"))//no data of type real
End if 