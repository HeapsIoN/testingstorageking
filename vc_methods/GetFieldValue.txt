//%attributes = {"lang":"en"} comment added and reserved by 4D.
C_TEXT($0)//String returned
C_POINTER($1;$p)//Pointer of field to return string value of
C_BOOLEAN($2;$Old)//If true return the old value
$Old:=False
If (Count parameters>=2)
$Old:=$2
End if 
$p:=$1
$0:=""
$Type:=Type($p->)
Case of 
: ($Type=Is picture)
$0:="Picture"
: ($Type=Is BLOB)
$0:="BLOB"
: ($Type=Is subtable)
$0:="Subtable"
: ($Type=Is alpha field) | ($Type=Is text)//Alpha,Text - OLD doesn't work on text fields!
If ($Old)
If ($Type=Is alpha field)
$0:=Old($p->)
End if 
Else 
$0:=$p->
End if 
If (Table($p)=26) & (Field name($p)="Password")
$0:=EncryptIt ($0;True)
End if 
: ($Type=Is real) | ($Type=Is integer) | ($Type=Is longint) | ($Type=Is time)
If ($Old)
$0:=String(Old($p->))
Else 
$0:=String($p->)
End if 
: ($Type=Is date)
If ($Old)
$0:=String(Old($p->);7)
Else 
$0:=String($p->;7)
End if 
Case of 
: (DateFormat =1)
$0:=$0+"US"
: (DateFormat =2)
$0:=$0+"ISO"
End case 
: ($Type=Is Boolean)
$0:="False"
If ($Old) & (Old($p->))
$0:="True"
End if 
If (Not($Old)) & ($p->)
$0:="True"
End if 
Else 
$0:="Type "+String($Type)+" is not supported!"
End case 